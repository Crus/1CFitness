////////////////////////////////////////////////////////////////////////////////
// Подсистема "Базовая функциональность".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции интерактивной работы.

// Устанавливает заголовок главного окна приложения, используя представление текущего пользователя,
// значение константы ЗаголовокПриложения и заголовок приложения по умолчанию.
//
// Параметры:
//   ПриЗапуске - Булево - Истина, если вызывается при начале работы программы.
//
Процедура УстановитьРасширенныйЗаголовокПриложения(ПриЗапуске = Ложь) Экспорт
	
	ПараметрыКлиента = ?(ПриЗапуске, СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске(),
		СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента());
		
	Если ПараметрыКлиента.ДоступноИспользованиеРазделенныхДанных Тогда
		ПредставлениеЗаголовка = ПараметрыКлиента.ЗаголовокПриложения;
		ПредставлениеПользователя = ПараметрыКлиента.ПредставлениеПользователя;
		ПредставлениеКонфигурации = ПараметрыКлиента.ПодробнаяИнформация;
		
		Если ПустаяСтрока(СокрЛП(ПредставлениеЗаголовка)) Тогда
			Если ПараметрыКлиента.Свойство("ПредставлениеОбластиДанных") Тогда
				ШаблонЗаголовка = "%1 / %2 / %3";
				ЗаголовокПриложения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, 
					ПараметрыКлиента.ПредставлениеОбластиДанных, ПредставлениеКонфигурации, 
					ПредставлениеПользователя);
			Иначе
				ШаблонЗаголовка = "%1 / %2";
				ЗаголовокПриложения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, 
					ПредставлениеКонфигурации, ПредставлениеПользователя);
			КонецЕсли;
		Иначе
			ШаблонЗаголовка = "%1 / %2 / %3";
			ЗаголовокПриложения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, 
				СокрЛП(ПредставлениеЗаголовка), ПредставлениеПользователя, ПредставлениеКонфигурации);
		КонецЕсли;
	Иначе
		ШаблонЗаголовка = "%1 / %2";
		ЗаголовокПриложения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, 
			НСтр("ru = 'Не установлены разделители'"), ПараметрыКлиента.ПодробнаяИнформация);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентПереопределяемый.ПриУстановкеЗаголовкаКлиентскогоПриложения(ЗаголовокПриложения, ПриЗапуске);
	
	УстановитьЗаголовокКлиентскогоПриложения(ЗаголовокПриложения);
	
КонецПроцедуры

// Показать форму вопроса.
//
// Параметры:
//   ОписаниеОповещенияОЗавершении - ОписаниеОповещения - описание процедуры, которая будет вызвана после закрытия окна
//                                                        вопроса
//                                                        со следующими параметрами: 
//                                                          РезультатВопроса - Структура - структура со свойствами:
//                                                            Значение - результат выбора пользователя: значение
//                                                                       системного перечисления или значение,
//                                                                       связанное с нажатой кнопкой. В случае закрытия
//                                                                       диалога по истечении времени - значение
//                                                                       Таймаут.
//                                                            БольшеНеЗадаватьЭтотВопрос - Булево - результат выбора
//                                                                                                  пользователя в
//                                                                                                  одноименном флажке.
//                                                          ДополнительныеПараметры - Структура 
//   ТекстВопроса                  - Строка             - текст задаваемого вопроса. 
//   Кнопки                        - РежимДиалогаВопрос, СписокЗначений - может быть задан список значений, в котором.
//                                       Значение - содержит значение, связанное с 
//                                                  кнопкой и возвращаемое при выборе кнопки. В качестве значения может
//                                                  использоваться значение
//                                                  перечисления КодВозвратаДиалога, а также другие значения,
//                                                  поддерживающее XDTO-сериализацию.
//                                       Представление - задает текст кнопки.
//
//   ДополнительныеПараметры       - Структура          - дополнительные параметры, см. описание к
//                                                        ПараметрыВопросаПользователю.
//
// Возвращаемое значение:
//   Результат выбора пользователя будет передан в метод, описанный параметром ОписаниеОповещенияОЗавершении. 
//
Процедура ПоказатьВопросПользователю(ОписаниеОповещенияОЗавершении, ТекстВопроса, Кнопки, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		Параметры = ДополнительныеПараметры;
	Иначе	
		Параметры = ПараметрыВопросаПользователю();
	КонецЕсли;
	
	Если ТипЗнч(Кнопки) = Тип("РежимДиалогаВопрос") Тогда
		Если      Кнопки = РежимДиалогаВопрос.ДаНет Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ДаНет";
		ИначеЕсли Кнопки = РежимДиалогаВопрос.ДаНетОтмена Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ДаНетОтмена";
		ИначеЕсли Кнопки = РежимДиалогаВопрос.ОК Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ОК";
		ИначеЕсли Кнопки = РежимДиалогаВопрос.ОКОтмена Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ОКОтмена";
		ИначеЕсли Кнопки = РежимДиалогаВопрос.ПовторитьОтмена Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ПовторитьОтмена";
		ИначеЕсли Кнопки = РежимДиалогаВопрос.ПрерватьПовторитьПропустить Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ПрерватьПовторитьПропустить";
		КонецЕсли;
	Иначе
		КнопкиПараметр = Кнопки;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.КнопкаПоУмолчанию) = Тип("КодВозвратаДиалога") Тогда
		КнопкаПоУмолчаниюПараметр = КодВозвратаДиалогаВСтроку(Параметры.КнопкаПоУмолчанию);
	Иначе
		КнопкаПоУмолчаниюПараметр = Параметры.КнопкаПоУмолчанию;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.КнопкаТаймаута) = Тип("КодВозвратаДиалога") Тогда
		КнопкаТаймаутаПараметр = КодВозвратаДиалогаВСтроку(Параметры.КнопкаТаймаута);
	Иначе
		КнопкаТаймаутаПараметр = Параметры.КнопкаТаймаута;
	КонецЕсли;
	
	Параметры.Вставить("Кнопки",            КнопкиПараметр);
	Параметры.Вставить("Таймаут",           Параметры.Таймаут);
	Параметры.Вставить("КнопкаПоУмолчанию", КнопкаПоУмолчаниюПараметр);
	Параметры.Вставить("Заголовок",         Параметры.Заголовок);
	Параметры.Вставить("КнопкаТаймаута",    КнопкаТаймаутаПараметр);
	Параметры.Вставить("ТекстСообщения",    ТекстВопроса);
	Параметры.Вставить("Картинка",          Параметры.Картинка);
	Параметры.Вставить("ПредлагатьБольшеНеЗадаватьЭтотВопрос", Параметры.ПредлагатьБольшеНеЗадаватьЭтотВопрос);
	
	ОткрытьФорму("ОбщаяФорма.Вопрос", Параметры,,,,,ОписаниеОповещенияОЗавершении);
	
КонецПроцедуры

// Возвращает новую структуру дополнительных параметров для процедуры ПоказатьВопросПользователю.
//
// Возвращаемое значение:
//  Структура   - структура со свойствами:
//    * КнопкаПоУмолчанию             - Произвольный - определяет кнопку по умолчанию по типу кнопки или по связанному
//                                                     с ней значению.
//    * Таймаут                       - Число        - интервал времени в секундах до автоматического закрытия окна
//                                                     вопроса.
//    * КнопкаТаймаута                - Произвольный - кнопка (по типу кнопки или по связанному с ней значению), 
//                                                     на которой отображается количество секунд, оставшихся до
//                                                     истечения таймаута.
//    * Заголовок                     - Строка       - заголовок вопроса. 
//    * ПредлагатьБольшеНеЗадаватьЭтотВопрос - Булево- если Истина, то в окне вопроса будет доступен одноименный флажок.
//    * БольшеНеЗадаватьЭтотВопрос    - Булево       - принимает значение, выбранное пользователем в соответствующем
//                                                     флажке.
//    * БлокироватьВесьИнтерфейс      - Булево       - если Истина, форма вопроса открывается блокируя работу всех
//                                                     остальных открытых окон, включая главное окно.
//    * Картинка                      - Картинка     - картинка, выводимая в окне вопроса.
//
Функция ПараметрыВопросаПользователю() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("КнопкаПоУмолчанию", Неопределено);
	Параметры.Вставить("Таймаут", 0);
	Параметры.Вставить("КнопкаТаймаута", Неопределено);
	Параметры.Вставить("Заголовок", ПолучитьЗаголовокКлиентскогоПриложения());
	Параметры.Вставить("ПредлагатьБольшеНеЗадаватьЭтотВопрос", Истина);
	Параметры.Вставить("БольшеНеЗадаватьЭтотВопрос", Ложь);
	Параметры.Вставить("БлокироватьВесьИнтерфейс", Ложь);
	Параметры.Вставить("Картинка", Неопределено);
	Возврат Параметры;
	
КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// Запуск и завершение работы.

// Отключает выдачу предупреждения пользователю при завершении работы программы.
//
Процедура ПропуститьПредупреждениеПередЗавершениемРаботыСистемы() Экспорт
	
	ПараметрыПриложения.Вставить("СтандартныеПодсистемы.ПропуститьПредупреждениеПередЗавершениемРаботыСистемы", Истина);
	
КонецПроцедуры

// Выполнить стандартные действия перед началом работы пользователя
// с областью данных, либо в локальном режиме работы.
//
// Предназначена для вызова из обработчика ПередНачаломРаботыСистемы модулей управляемого и обычного приложения.
//
// Параметры:
//  ОповещениеЗавершения - ОписаниеОповещения - не задается при вызове из обработчика ПередНачаломРаботыСистемы 
//                         модулей управляемого и обычного приложения. В других случаях, после запуска будет вызвано
//                         оповещение с параметром типа Структура со свойствами:
//                         - Отказ - Булево - Ложь, если запуск выполнен успешно, или Истина, если вход в программу не
//                         выполнен;
//                         - Перезапустить - Булево - если требуется перезапуск программы;
//                         - ДополнительныеПараметрыКоманднойСтроки - Строка - для перезапуска.
//
Процедура ПередНачаломРаботыСистемы(Знач ОповещениеЗавершения = Неопределено) Экспорт
	
	Если ПараметрыПриложения = Неопределено Тогда
		ПараметрыПриложения = Новый Соответствие;
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("СтандартныеПодсистемыКлиент.ПередНачаломРаботыСистемы", 
			"ОповещениеЗавершения", ОповещениеЗавершения, Тип("ОписаниеОповещения"));
	КонецЕсли;
	
	УстановитьРазделениеСеанса();
	
	Параметры = Новый Структура;
	
	// Внешние параметры описания результата.
	Параметры.Вставить("Отказ", Ложь);
	Параметры.Вставить("Перезапустить", Ложь);
	Параметры.Вставить("ДополнительныеПараметрыКоманднойСтроки", "");
	
	// Внешние параметры управления выполнением.
	Параметры.Вставить("ИнтерактивнаяОбработка", Неопределено); // ОписаниеОповещения.
	Параметры.Вставить("ОбработкаПродолжения",   Неопределено); // ОписаниеОповещения.
	Параметры.Вставить("НепрерывноеВыполнение", Истина);
	Параметры.Вставить("ПолученныеПараметрыКлиента", Новый Структура);
	
	// Внутренние параметры.
	Параметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	Параметры.Вставить("ОбработкаЗавершения", Новый ОписаниеОповещения(
		"ДействияПередНачаломРаботыСистемыОбработкаЗавершения", ЭтотОбъект, Параметры));
	
	ОбновитьПараметрыРаботыКлиента(Параметры, Истина, ОповещениеЗавершения <> Неопределено);
	ПараметрыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	
	Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
		"ДействияПередНачаломРаботыСистемыПослеПроверкиВерсииПлатформы", ЭтотОбъект, Параметры));
	
	Попытка
		ПроверитьВерсиюПлатформыПриЗапуске(Параметры);
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Выполнить стандартные действия при начале работы пользователя
// с областью данных, либо в локальном режиме работы.
//
// Предназначена для вызова из обработчика ПриНачалеРаботыСистемы модулей управляемого и обычного приложения.
//
// Параметры:
//  ОповещениеЗавершения - ОписаниеОповещения - не задается при вызове из обработчика ПриНачалеРаботыСистемы 
//                         модулей управляемого и обычного приложения. В других случаях, после запуска будет вызвано
//                         оповещение с параметром типа Структура со свойствами:
//                         - Отказ - Булево - Ложь, если запуск выполнен успешно, или Истина, если вход в программу не
//                         выполнен;
//                         - Перезапустить - Булево - если требуется перезапуск программы;
//                         - ДополнительныеПараметрыКоманднойСтроки - Строка - для перезапуска.
//
//  НепрерывноеВыполнение - Булево - Только для внутреннего использования.
//                          Для перехода из обработчика ПередНачаломРаботыСистемы
//                          выполненного в режиме интерактивной обработки.
//
Процедура ПриНачалеРаботыСистемы(Знач ОповещениеЗавершения = Неопределено, НепрерывноеВыполнение = Истина) Экспорт
	
	Если ОповещениеЗавершения <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("СтандартныеПодсистемыКлиент.ПриНачалеРаботыСистемы", 
			"ОповещениеЗавершения", ОповещениеЗавершения, Тип("ОписаниеОповещения"));
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("СтандартныеПодсистемыКлиент.ПриНачалеРаботыСистемы", 
		"НепрерывноеВыполнение", НепрерывноеВыполнение, Тип("Булево"));
	
	Если ВыполняетсяИнтерактивнаяОбработкаПередНачаломРаботыСистемы() Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = Новый Структура;
	
	// Внешние параметры описания результата.
	Параметры.Вставить("Отказ", Ложь);
	Параметры.Вставить("Перезапустить", Ложь);
	Параметры.Вставить("ДополнительныеПараметрыКоманднойСтроки", "");
	
	// Внешние параметры управления выполнением.
	Параметры.Вставить("ИнтерактивнаяОбработка", Неопределено); // ОписаниеОповещения.
	Параметры.Вставить("ОбработкаПродолжения",   Неопределено); // ОписаниеОповещения.
	Параметры.Вставить("НепрерывноеВыполнение", НепрерывноеВыполнение);
	
	// Внутренние параметры.
	Параметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	Параметры.Вставить("ОбработкаЗавершения", Новый ОписаниеОповещения(
		"ДействияПриНачалеРаботыСистемыОбработкаЗавершения", ЭтотОбъект, Параметры));
	
	// Подготовка перехода к следующей процедуре.
	Параметры.Вставить("НомерСледующегоОбработчика", 1);
	Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
		"ДействияПриНачалеРаботыСистемыВОбработчикахСлужебногоСобытия", ЭтотОбъект, Параметры));
	
	Попытка
		УстановитьРасширенныйЗаголовокПриложения(Истина); // Для главного окна.
		
		Если НЕ ОбработатьПараметрыЗапуска() Тогда
			Параметры.Отказ = Истина;
			ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения);
			Возврат;
		КонецЕсли;
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПриНачалеРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Выполнить стандартные действия перед завершением работы пользователя
// с областью данных, либо в локальном режиме работы.
//
// Предназначена для вызова из обработчика ПередЗавершениемРаботыСистемы модулей управляемого и обычного приложения.
//
// Параметры:
//  Отказ                - Булево - Возвращаемое значение. Признак отказа от завершения работы 
//                         для обработчика события ПередЗавершениемРаботыСистемы, либо программного отказа,
//                         либо потребовалась интерактивная обработка. В случае успешного взаимодействия
//                         с пользователем, завершение работы будет продолжено.
//
//  ОповещениеЗавершения - ОписаниеОповещения - не задается при вызове из обработчика ПередЗавершениемРаботыСистемы 
//                         модулей управляемого и обычного приложения. В других случаях, при завершении будет вызвано
//                         оповещение с параметром типа Структура со свойством:
//                         - Отказ - Булево - Ложь, если завершение работы выполнено успешно, 
//                                   или Истина, если завершение работы следует отменить;
//
Процедура ПередЗавершениемРаботыСистемы(Отказ = Ложь, Знач ОповещениеЗавершения = Неопределено) Экспорт
	
	ПараметрыПриЗапускеИЗавершенииПрограммы = ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы"];
	
	Если ПараметрыПриЗапускеИЗавершенииПрограммы.Свойство("СкрытьРабочийСтолПриНачалеРаботыСистемы") Тогда
		// Произошла попытка закрытия до окончания запуска.
	#Если ВебКлиент Тогда
		// В веб-клиенте это возможно в штатном случае (при закрытии страницы в целом),
		// поэтому закрытие блокируется, так как его все равно можно выполнить принудительно,
		// а в случае случайного закрытия у пользователя должна быть возможность остаться на странице.
		// Исключением является выход из области данных, т.е. когда "ОповещениеЗавершения <> Неопределено".
		Если ОповещениеЗавершения = Неопределено Тогда
			Отказ = Истина;
		КонецЕсли;
	#Иначе
		// Не в веб-клиенте это возможно в случае ошибок в немодальной последовательности запуска.
		// То есть нет ни одного окна блокирующего весь интерфейс. Закрытие нужно разрешить,
		// но без стандартных процедур перед завершение работы системы, так как они могут
		// привести к ошибке в процессе закрытия из-за незавершенного запуска.
	#КонецЕсли
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		"СтандартныеПодсистемыКлиент.ПередЗавершениемРаботыСистемы", "Отказ", Отказ, Тип("Булево"));
	
	Если ОповещениеЗавершения <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("СтандартныеПодсистемыКлиент.ПередЗавершениемРаботыСистемы", 
			"ОповещениеЗавершения", ОповещениеЗавершения, Тип("ОписаниеОповещения"));
	КонецЕсли;
	
	Если ОповещениеЗавершения = Неопределено Тогда
		Если ПараметрыПриЗапускеИЗавершенииПрограммы.Свойство("ДействияПередЗавершениемРаботыСистемыВыполнены") Тогда
			ПараметрыПриЗапускеИЗавершенииПрограммы.Удалить("ДействияПередЗавершениемРаботыСистемыВыполнены");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// При очередном выходе из приложения параметры клиента при завершении нужно получать заново.
	Если ПараметрыПриЗапускеИЗавершенииПрограммы.Свойство("ПараметрыРаботыКлиентаПриЗавершении") Тогда
		ПараметрыПриЗапускеИЗавершенииПрограммы.Удалить("ПараметрыРаботыКлиентаПриЗавершении");
	КонецЕсли;
	
	Параметры = Новый Структура;
	
	// Внешние параметры описания результата.
	Параметры.Вставить("Отказ", Ложь);
	
	// Внешние параметры управления выполнением.
	Параметры.Вставить("ИнтерактивнаяОбработка", Неопределено); // ОписаниеОповещения.
	Параметры.Вставить("ОбработкаПродолжения",   Неопределено); // ОписаниеОповещения.
	Параметры.Вставить("НепрерывноеВыполнение", Истина);
	
	// Внутренние параметры.
	Параметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	Параметры.Вставить("ОбработкаЗавершения", Новый ОписаниеОповещения(
		"ДействияПередЗавершениемРаботыСистемыОбработкаЗавершения", ЭтотОбъект, Параметры));
	
	Параметры.Вставить("НомерСледующегоОбработчика", 1);
	Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
		"ДействияПередЗавершениемРаботыСистемыВОбработчикахСлужебногоСобытия", ЭтотОбъект, Параметры));
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
	Если Параметры.Отказ Или НЕ Параметры.НепрерывноеВыполнение Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру параметров, необходимых для работы
// конфигурации на клиенте при завершении, т.е. в обработчиках событий.
// - ПередЗавершениемРаботыСистемы,
// - ПриЗавершенииРаботыСистемы
// 
// Возвращаемое значение:
//   ФиксированнаяСтруктура - структура параметров работы клиента при завершении.
//
Функция ПараметрыРаботыКлиентаПриЗавершении() Экспорт
	
	ПараметрыПриЗапускеИЗавершенииПрограммы = ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы"];
	
	Если НЕ ПараметрыПриЗапускеИЗавершенииПрограммы.Свойство("ПараметрыРаботыКлиентаПриЗавершении") Тогда
		ПараметрыПриЗапускеИЗавершенииПрограммы.Вставить("ПараметрыРаботыКлиентаПриЗавершении",
			СтандартныеПодсистемыВызовСервера.ПараметрыРаботыКлиентаПриЗавершении());
	КонецЕсли;
	
	Возврат ПараметрыПриЗапускеИЗавершенииПрограммы.ПараметрыРаботыКлиентаПриЗавершении;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Поиск и удаление дублей.

// Устарела. Следует использовать ПоискИУдалениеДублейКлиент.ОбъединитьВыделенные.
//
Процедура ОбъединитьВыделенные(Знач ОбъединяемыеЭлементы) Экспорт
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПоискИУдалениеДублей") Тогда
		МодульПоискИУдалениеДублейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПоискИУдалениеДублейКлиент");
		МодульПоискИУдалениеДублейКлиент.ОбъединитьВыделенные(ОбъединяемыеЭлементы);
	КонецЕсли;
КонецПроцедуры

// Устарела. Следует использовать ПоискИУдалениеДублейКлиент.ЗаменитьВыделенные.
//
Процедура ЗаменитьВыделенные(Знач ОбъединяемыеЭлементы) Экспорт
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПоискИУдалениеДублей") Тогда
		МодульПоискИУдалениеДублейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПоискИУдалениеДублейКлиент");
		МодульПоискИУдалениеДублейКлиент.ЗаменитьВыделенные(ОбъединяемыеЭлементы);
	КонецЕсли;
КонецПроцедуры

// Устарела. Следует использовать ПоискИУдалениеДублейКлиент.ПоказатьМестаИспользования.
//
Процедура ПоказатьМестаИспользования(Знач Элементы, Знач ПараметрыОткрытия = Неопределено) Экспорт
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПоискИУдалениеДублей") Тогда
		МодульПоискИУдалениеДублейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПоискИУдалениеДублейКлиент");
		МодульПоискИУдалениеДублейКлиент.ПоказатьМестаИспользования(Элементы, ПараметрыОткрытия);
	КонецЕсли;
КонецПроцедуры

// ИспользованиеМодальности

// Устарела. Следует использовать ПоказатьВопросПользователю.
// Вызывает форму вопроса.
// Параметры и возвращаемое значение совместимы с функцией глобального контекста Вопрос со следующими дополнениями:
//
// Параметры:
//   Кнопки - СписокЗначений - в котором:
//            Значение - содержит значение, связанное с 
//              кнопкой и возвращаемое при выборе кнопки. В качестве значения может использоваться значение 
//              перечисления КодВозвратаДиалога, а также другие значения, поддерживающее XDTO сериализацию.
//            Представление - задает текст кнопки.
//
//   БольшеНеЗадаватьЭтотВопрос - Булево - принимает значение, выбранное пользователем в соответствующим 
//                                         флажке диалога.
//
//   ПредлагатьБольшеНеЗадаватьЭтотВопрос - Булево - флаг того, что пользователю надо предлагать вариант.
//
// Возвращаемое значение:
//   КодВозвратаДиалога - ответ пользователя. Либо одно из значений из списка значений Кнопки. 
//
Функция ВопросПользователю(ТекстСообщения, Кнопки, Таймаут = 0, КнопкаПоУмолчанию = Неопределено, Заголовок = "", КнопкаТаймаута = Неопределено,
	БольшеНеЗадаватьЭтотВопрос = Ложь, ПредлагатьБольшеНеЗадаватьЭтотВопрос=Истина) Экспорт
	
	БольшеНеЗадаватьЭтотВопрос = Ложь;
	
	Параметры = Новый Структура;
	
	Если ТипЗнч(Кнопки) = Тип("РежимДиалогаВопрос") Тогда
		Если      Кнопки = РежимДиалогаВопрос.ДаНет Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ДаНет";
		ИначеЕсли Кнопки = РежимДиалогаВопрос.ДаНетОтмена Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ДаНетОтмена";
		ИначеЕсли Кнопки = РежимДиалогаВопрос.ОК Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ОК";
		ИначеЕсли Кнопки = РежимДиалогаВопрос.ОКОтмена Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ОКОтмена";
		ИначеЕсли Кнопки = РежимДиалогаВопрос.ПовторитьОтмена Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ПовторитьОтмена";
		ИначеЕсли Кнопки = РежимДиалогаВопрос.ПрерватьПовторитьПропустить Тогда
			КнопкиПараметр = "РежимДиалогаВопрос.ПрерватьПовторитьПропустить";
		КонецЕсли;
	Иначе
		КнопкиПараметр = Кнопки;
	КонецЕсли;
	
	Если ТипЗнч(КнопкаПоУмолчанию) = Тип("КодВозвратаДиалога") Тогда
		КнопкаПоУмолчаниюПараметр = КодВозвратаДиалогаВСтроку(КнопкаПоУмолчанию);
	Иначе
		КнопкаПоУмолчаниюПараметр = КнопкаПоУмолчанию;
	КонецЕсли;
	
	Если ТипЗнч(КнопкаТаймаута) = Тип("КодВозвратаДиалога") Тогда
		КнопкаТаймаутаПараметр = КодВозвратаДиалогаВСтроку(КнопкаТаймаута);
	Иначе
		КнопкаТаймаутаПараметр = КнопкаТаймаута;
	КонецЕсли;
	
	Параметры.Вставить("Кнопки",            КнопкиПараметр);
	Параметры.Вставить("Таймаут",           Таймаут);
	Параметры.Вставить("КнопкаПоУмолчанию", КнопкаПоУмолчаниюПараметр);
	Параметры.Вставить("Заголовок",         Заголовок);
	Параметры.Вставить("КнопкаТаймаута",    КнопкаТаймаутаПараметр);
	Параметры.Вставить("ТекстСообщения",    ТекстСообщения);
	
	Параметры.Вставить("ПредлагатьБольшеНеЗадаватьЭтотВопрос", ПредлагатьБольшеНеЗадаватьЭтотВопрос);
	
	Результат = ОткрытьФормуМодально("ОбщаяФорма.Вопрос", Параметры);
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		БольшеНеЗадаватьЭтотВопрос = Результат.БольшеНеЗадаватьЭтотВопрос;
		Возврат Результат.Значение;
	Иначе
		Возврат КодВозвратаДиалога.Отмена;
	КонецЕсли;
	
КонецФункции

// Конец ИспользованиеМодальности

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает новую структуру параметров для вывода предупреждения перед завершением работы программы.
//
// Возвращаемое значение:
//  Структура - со свойствами:
//    ТекстФлажка      - Строка - текст флажка.
//    ПоясняющийТекст  - Строка - текст, выводимый в форме сверху управляющего элемента (флажок или гиперссылка).
//    ТекстГиперссылки - Строка - текст гиперссылки.
//    РасширеннаяПодсказка - Строка - текст подсказки, выводимый по кнопке справа от управляющего элемента (флажок или
//                                    гиперссылка).
//    Приоритет        - Число  - определяет относительный порядок вывода предупреждений в форме (чем больше, тем выше).
//    ВывестиОдноПредупреждение - Булево - если Истина, то в списке предупреждений выводится только одно это
//                                         предупреждение.
//    ДействиеПриУстановленномФлажке - Структура с полями:
//      * Форма          - Строка    - путь к открываемой форме.
//      * ПараметрыФормы - Структура - произвольная структура параметров формы Форма. 
//    ДействиеПриНажатииГиперссылки - Структура с полями:
//      * Форма          - Строка    - путь к форме, которая должна открываться по нажатию на гиперссылку.
//      * ПараметрыФормы - Структура - произвольная структура параметров для вышеописанной формы.
//      * ПрикладнаяФормаПредупреждения - Строка - путь к форме, которая должна открываться сразу
//                                        вместо универсальной формы в случае, когда в списке 
//                                        предупреждений оказывается только одно данное предупреждение.
//      * ПараметрыПрикладнойФормыПредупреждения - Структура - произвольная структура
//                                                 параметров для вышеописанной формы.
Функция ПредупреждениеПриЗавершениеРаботы() Экспорт
	
	ДействиеПриУстановленномФлажке = Новый Структура;
	ДействиеПриУстановленномФлажке.Вставить("Форма", "");
	ДействиеПриУстановленномФлажке.Вставить("ПараметрыФормы", Неопределено);
	
	ДействиеПриНажатииГиперссылки = Новый Структура;
	ДействиеПриНажатииГиперссылки.Вставить("Форма", "");
	ДействиеПриНажатииГиперссылки.Вставить("ПараметрыФормы", Неопределено);
	ДействиеПриНажатииГиперссылки.Вставить("ПрикладнаяФормаПредупреждения", "");
	ДействиеПриНажатииГиперссылки.Вставить("ПараметрыПрикладнойФормыПредупреждения", Неопределено);
	
	ПараметрыПредупреждения = Новый Структура;
	ПараметрыПредупреждения.Вставить("ТекстФлажка", "");
	ПараметрыПредупреждения.Вставить("ПоясняющийТекст", "");
	ПараметрыПредупреждения.Вставить("РасширеннаяПодсказка", "");
	ПараметрыПредупреждения.Вставить("ТекстГиперссылки", "");
	ПараметрыПредупреждения.Вставить("ДействиеПриУстановленномФлажке", ДействиеПриУстановленномФлажке);
	ПараметрыПредупреждения.Вставить("ДействиеПриНажатииГиперссылки", ДействиеПриНажатииГиперссылки);
	ПараметрыПредупреждения.Вставить("Приоритет", 0);
	ПараметрыПредупреждения.Вставить("ВывестиОдноПредупреждение", Ложь);
	
	Возврат ПараметрыПредупреждения;
	
КонецФункции	

// После предупреждения вызывает процедуру с параметрами Результат, ДополнительныеПараметры.
//
// Параметры:
//  Параметры           - Структура, которая содержит свойство:
//                          ОбработкаПродолжения - ОписаниеОповещения, которое
//                          содержит процедуру с двумя параметрами:
//                            Результат, ДополнительныеПараметры.
//
//  ТекстПредупреждения - Строка - текст предупреждения, который нужно показать.
//
Процедура ПоказатьПредупреждениеИПродолжить(Параметры, ТекстПредупреждения) Экспорт
	
	ОповещениеСРезультатом = Параметры.ОбработкаПродолжения;
	
	Если ТекстПредупреждения = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеСРезультатом);
		Возврат;
	КонецЕсли;
		
	Если Параметры.Отказ Тогда
		
		Кнопки = Новый СписокЗначений();
		Кнопки.Добавить("Перезапустить", НСтр("ru = 'Перезапустить'"));
		Кнопки.Добавить("Завершить",     НСтр("ru = 'Завершить работу'"));
		
		ПараметрыВопроса = ПараметрыВопросаПользователю();
		ПараметрыВопроса.КнопкаПоУмолчанию = "Перезапустить";
		ПараметрыВопроса.КнопкаТаймаута    = "Перезапустить";
		ПараметрыВопроса.Таймаут = 60;
		ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		ПараметрыВопроса.БлокироватьВесьИнтерфейс = Истина;
		ПараметрыВопроса.Картинка = БиблиотекаКартинок.Предупреждение32;
		
	Иначе

		Кнопки = Новый СписокЗначений();
		Кнопки.Добавить("Продолжить", НСтр("ru = 'Продолжить'"));
		Если Параметры.Свойство("Перезапустить") Тогда
			Кнопки.Добавить("Перезапустить", НСтр("ru = 'Перезапустить'"));
		КонецЕсли;
		Кнопки.Добавить("Завершить", НСтр("ru = 'Завершить работу'"));
		
		ПараметрыВопроса = ПараметрыВопросаПользователю();
		ПараметрыВопроса.КнопкаПоУмолчанию = "Продолжить";
		ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		ПараметрыВопроса.БлокироватьВесьИнтерфейс = Истина;
		ПараметрыВопроса.Картинка = БиблиотекаКартинок.Предупреждение32;
		
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПоказатьПредупреждениеИПродолжитьЗавершение", ЭтотОбъект, Параметры);
	ПоказатьВопросПользователю(ОповещениеОЗакрытии, ТекстПредупреждения, Кнопки, ПараметрыВопроса);
	
КонецПроцедуры

// Показывает диалог выбора файлов и помещает выбранные файлы во временное хранилище.
//  Совмещает работу методов глобального метода НачатьПомещениеФайла и ПоместитьФайлы,
//  возвращая идентичный результат вне зависимости от того, подключено расширение работы с файлами, или нет.
//
// Параметры:
//   ОбработчикЗавершения  - ОписаниеОповещения - Описание процедуры, принимающей результат выбора.
//   ИдентификаторФормы    - УникальныйИдентификатор - Уникальный идентификатор формы, из которой выполняется
//                                                     размещение файла.
//   НачальноеИмяФайла     - Строка - Полный путь и имя файла, которые будут предложены пользователю в начале выбора.
//   ПараметрыДиалога      - Структура, Неопределено - См. свойства ДиалогВыбораФайла в синтакс-помощнике.
//       Используется в случае, если удалось подключить расширение работы с файлами.
//
// Значение первого параметра, возвращаемого в ОбработчикРезультата:
//   ПомещенныеФайлы - Результат выбора.
//       * - Неопределено - Пользователь отказался от выбора.
//       * - Массив из ОписаниеПереданногоФайла, Структура - Пользователь выбрал файл.
//           ** Имя      - Строка - Полное имя выбранного файла.
//           ** Хранение - Строка - Адрес во временном хранилище, по которому размещен файл.
//
// Ограничения:
//   Используется только для интерактивного выбора в диалоге.
//   Не используется для выбора каталогов - эта опция не поддерживается веб-клиентом.
//   Не поддерживается множественный выбор в веб-клиенте, если не установлено расширение работы с файлами.
//   Не поддерживается передача адреса временного хранилища.
//
Процедура ПоказатьПомещениеФайла(ОбработчикЗавершения, ИдентификаторФормы, НачальноеИмяФайла, ПараметрыДиалога) Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("ОбработчикЗавершения", ОбработчикЗавершения);
	Параметры.Вставить("ИдентификаторФормы", ИдентификаторФормы);
	Параметры.Вставить("НачальноеИмяФайла", НачальноеИмяФайла);
	Параметры.Вставить("ПараметрыДиалога", ПараметрыДиалога);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьПомещениеФайлаПриПодключенииРасширенияРаботыСФайлами", ЭтотОбъект, Параметры);
	ОбщегоНазначенияКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(ОписаниеОповещения);
КонецПроцедуры

// Открыть диалог для ввода логина и пароля пользователя Интернет-поддержки.
//
Процедура АвторизоватьНаСайтеПоддержкиПользователей(ВладелецФормы = Неопределено, ОповещениеОЗакрытии = Неопределено) Экспорт
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтернетПоддержкаПользователейКлиент");
		МодульИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОповещениеОЗакрытии);
	Иначе	
		ОткрытьФорму("ОбщаяФорма.АвторизацияНаСайтеПоддержкиПользователей",, ВладелецФормы,,,, ОповещениеОЗакрытии);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает имя исполняемого файла в зависимости от вида клиента и версии платформы.
//
Функция ИмяИсполняемогоФайлаПриложения(ПолучитьФайлаКонфигуратора = Ложь) Экспорт
	
	ШаблонИмениФайла = "1cv8[ТонкийКлиент][УчебнаяПлатформа].exe";
	
	ЭтоУчебнаяПлатформа = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ЭтоУчебнаяПлатформа;
	
	#Если ТонкийКлиент Тогда
		ЭтоТонкийКлиент = Не ПолучитьФайлаКонфигуратора;
	#Иначе
		ЭтоТонкийКлиент = Ложь;
	#КонецЕсли
	
	ИмяФайла = СтрЗаменить(ШаблонИмениФайла, "[ТонкийКлиент]", ?(ЭтоТонкийКлиент, "c", ""));
	ИмяФайла = СтрЗаменить(ИмяФайла, "[УчебнаяПлатформа]", ?(ЭтоУчебнаяПлатформа, "t", ""));
	
	Возврат ИмяФайла;
	
КонецФункции

// Устанавливает / отменяет хранение ссылки на управляемую форму в глобальной переменной.
// Требуется для случаев, когда ссылка на форму передается через ДополнительныеПараметры
// в объекте ОписаниеОповещения, который не блокирует освобождение закрытой формы.
//
Процедура УстановитьХранениеФормы(Форма, Хранение) Экспорт
	
#Если ВебКлиент Тогда
	Хранилище = ПараметрыПриложения["СтандартныеПодсистемы.ВременноеХранилищеСсылокНаУправляемыеФормы"];
	Если Хранилище = Неопределено Тогда
		Хранилище = Новый Соответствие;
		ПараметрыПриложения.Вставить("СтандартныеПодсистемы.ВременноеХранилищеСсылокНаУправляемыеФормы", Хранилище);
	КонецЕсли;
	
	Если Хранение Тогда
		Хранилище.Вставить(Форма, Новый Структура("Форма", Форма));
	ИначеЕсли Хранилище.Получить(Форма) <> Неопределено Тогда
		Хранилище.Удалить(Форма);
	КонецЕсли;
#КонецЕсли

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработка результата выполнения.

// Выводит результат выполнения какой-либо операции.
//
// Предназначена только для отображения результата работы сервера на клиенте,
// не предназначена для отображения промежуточных стадий - диалогов и т.п.
//
// См. также:
//   СтандартныеПодсистемыКлиентСервер.НовыйРезультатВыполнения()
//   СтандартныеПодсистемыКлиентСервер.ОповеститьДинамическиеСписки()
//   СтандартныеПодсистемыКлиентСервер.ВывестиПредупреждение()
//   СтандартныеПодсистемыКлиентСервер.ВывестиСообщение()
//   СтандартныеПодсистемыКлиентСервер.ВывестиОповещение()
//   СтандартныеПодсистемыКлиентСервер.РазвернутьУзлыДерева()
//
// Параметры:
//   Форма - УправляемаяФорма - Форма, для которой требуется вывод.
//   Результат - Структура - Результат выполнения операции, который необходимо вывести.
//       * ВыводОповещения - Структура - Всплывающее оповещение.
//           ** Использование - Булево - Выводить оповещение.
//           ** Заголовок     - Строка - Заголовок оповещения.
//           ** Текст         - Строка - Текст оповещения.
//           ** Ссылка        - Строка - Навигационная гиперссылка текста.
//           ** Картинка      - Картинка - Картинка оповещения.
//       * ВыводСообщения - Структура - Сообщение формы, привязанное к реквизиту.
//           ** Использование       - Булево - Выводить сообщение.
//           ** Текст               - Строка - Текст сообщения.
//           ** ПутьКРеквизитуФормы - Строка - Путь к реквизиту формы, к которому относится сообщение.
//       * ВыводПредупреждения - Структура - Окно предупреждения, блокирующее весь интерфейс.
//           ** Использование       - Булево - Выводить предупреждение.
//           ** Заголовок           - Строка - Заголовок окна.
//           ** Текст               - Строка - Текст предупреждения.
//           ** ТекстОшибок         - Строка - Необязательный. Тексты ошибок, которые при желании может просмотреть
//                                             пользователь.
//           ** ПутьКРеквизитуФормы - Строка - Необязательный. Путь к реквизиту формы, значение которого вызывало
//                                             ошибку.
//       * ОповещениеФорм - Структура, Массив из Структура - см. справку к методу глобального контекста Оповестить().
//           ** Использование - Булево - Оповещать открытые формы.
//           ** ИмяСобытия    - Строка - Имя события, которое используется для первичной идентификации сообщений
//                                       принимающими формами.
//           ** Параметр      - Произвольный - Набор данных, которые используются принимающей формой для обновления
//                                             состава.
//           ** Источник      - Произвольный - Источник оповещения, например форма-источником.
//       * ОповещениеДинамическихСписков - Структура - см. справку к методу глобального контекста
//                                                     ОповеститьОбИзменении().
//           ** Использование - Булево - Оповещать динамические списки.
//           ** СсылкаИлиТип  - Произвольный - Ссылка, тип, или массив типов, которые необходимо обновить.
//   ОбработчикЗавершения - ОписаниеОповещения - Описание процедуры, которая будет вызвана после завершения показа (со
//                                               значением Неопределено).
//
Процедура ПоказатьРезультатВыполнения(Форма, Результат, ОбработчикЗавершения = Неопределено) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") И ТипЗнч(Результат) <> Тип("ФиксированнаяСтруктура") Тогда
		Возврат;
	КонецЕсли;
	
	ОбработчикЗавершенияБудетВыполнен = Ложь;
	
	Если Результат.Свойство("ВыводОповещения") И Результат.ВыводОповещения.Использование Тогда
		Оповещение = Результат.ВыводОповещения;
		ПоказатьОповещениеПользователя(Оповещение.Заголовок, Оповещение.Ссылка, Оповещение.Текст, Оповещение.Картинка);
	КонецЕсли;
	
	Если Результат.Свойство("ВыводСообщения") И Результат.ВыводСообщения.Использование Тогда
		Сообщение = Новый СообщениеПользователю;
		Если ТипЗнч(Форма) = Тип("УправляемаяФорма") Тогда
			Сообщение.ИдентификаторНазначения = Форма.УникальныйИдентификатор;
		КонецЕсли;
		Сообщение.Текст = Результат.ВыводСообщения.Текст;
		Сообщение.Поле  = Результат.ВыводСообщения.ПутьКРеквизитуФормы;
		Сообщение.Сообщить();
	КонецЕсли;
	
	Если Результат.Свойство("ВыводПредупреждения") И Результат.ВыводПредупреждения.Использование Тогда
		ВыводПредупреждения = Результат.ВыводПредупреждения;
		Если ЗначениеЗаполнено(ВыводПредупреждения.ТекстОшибок) Тогда
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить(1, НСтр("ru = 'Подробнее...'"));
			Если ТипЗнч(Форма) = Тип("УправляемаяФорма") И ЗначениеЗаполнено(ВыводПредупреждения.ПутьКРеквизитуФормы) Тогда
				Кнопки.Добавить(2, НСтр("ru = 'Перейти к реквизиту'"));
			КонецЕсли;
			Кнопки.Добавить(0, НСтр("ru = 'Продолжить'"));
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ВыводПредупреждения",   ВыводПредупреждения);
			ДополнительныеПараметры.Вставить("Форма",                 Форма);
			ДополнительныеПараметры.Вставить("ОбработчикЗавершения", ОбработчикЗавершения);
			Обработчик = Новый ОписаниеОповещения("ПоказатьРезультатВыполненияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			
			ПоказатьВопрос(Обработчик, ВыводПредупреждения.Текст, Кнопки, , 1, ВыводПредупреждения.Заголовок);
			ОбработчикЗавершенияБудетВыполнен = Истина;
		Иначе
			ВернутьРезультатПослеПоказаПредупреждения(ВыводПредупреждения.Текст, ОбработчикЗавершения, Неопределено, ВыводПредупреждения.Заголовок);
			ОбработчикЗавершенияБудетВыполнен = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Результат.Свойство("ОповещениеФорм") Тогда
		Если ТипЗнч(Результат.ОповещениеФорм) = Тип("Структура") Или ТипЗнч(Результат.ОповещениеФорм) = Тип("ФиксированнаяСтруктура") Тогда
			ОповещениеФорм = Результат.ОповещениеФорм;
			Если ОповещениеФорм.Использование Тогда
				Оповестить(ОповещениеФорм.ИмяСобытия, ОповещениеФорм.Параметр, ОповещениеФорм.Источник);
			КонецЕсли;
		Иначе
			Для Каждого ОповещениеФорм Из Результат.ОповещениеФорм Цикл
				Если ОповещениеФорм.Использование Тогда
					Оповестить(ОповещениеФорм.ИмяСобытия, ОповещениеФорм.Параметр, ОповещениеФорм.Источник);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если Результат.Свойство("ОповещениеДинамическихСписков") И Результат.ОповещениеДинамическихСписков.Использование Тогда
		Если ТипЗнч(Результат.ОповещениеДинамическихСписков.СсылкаИлиТип) = Тип("Массив") Тогда
			Для Каждого СсылкаИлиТип Из Результат.ОповещениеДинамическихСписков.СсылкаИлиТип Цикл
				ОповеститьОбИзменении(СсылкаИлиТип);
			КонецЦикла;
		Иначе
			ОповеститьОбИзменении(Результат.ОповещениеДинамическихСписков.СсылкаИлиТип);
		КонецЕсли;
	КонецЕсли;
	
	Если Результат.Свойство("РазворачиваемыеУзлы") Тогда
		Для Каждого РазворачиваемыйУзел Из Результат.РазворачиваемыеУзлы Цикл
			ТаблицаЭлемент = Форма.Элементы[РазворачиваемыйУзел.ИмяТаблицы];
			Если РазворачиваемыйУзел.Идентификатор = "*" Тогда
				Узлы = Форма[РазворачиваемыйУзел.ИмяТаблицы].ПолучитьЭлементы();
				Для Каждого Узел Из Узлы Цикл
					ТаблицаЭлемент.Развернуть(Узел.ПолучитьИдентификатор(), РазворачиваемыйУзел.СПодчиненными);
				КонецЦикла;
			Иначе
				ТаблицаЭлемент.Развернуть(РазворачиваемыйУзел.Идентификатор, РазворачиваемыйУзел.СПодчиненными);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ОбработчикЗавершенияБудетВыполнен И ОбработчикЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОбработчикЗавершения, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик ответа на вопрос при выводе результата выполнения.
//
Процедура ПоказатьРезультатВыполненияЗавершение(Ответ, Результат) Экспорт
	
	ОбработчикЗавершенияБудетВыполнен = Ложь;
	Если ТипЗнч(Ответ) = Тип("Число") Тогда
		Если Ответ = 1 Тогда
			ПолныйТекст = Строка(Результат.ВыводПредупреждения.Текст) + Символы.ПС + Символы.ПС + Результат.ВыводПредупреждения.ТекстОшибок;
			Заголовок = Результат.ВыводПредупреждения.Заголовок;
			Если ПустаяСтрока(Заголовок) Тогда
				Заголовок = НСтр("ru = 'Подробнее'");
			КонецЕсли;
			Обработчик = Новый ОписаниеОповещения("ПоказатьРезультатВыполненияЗавершение", ЭтотОбъект, Результат);
			ПоказатьВводСтроки(Обработчик, ПолныйТекст, Заголовок, , Истина);
			ОбработчикЗавершенияБудетВыполнен = Истина;
		ИначеЕсли Ответ = 2 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.ИдентификаторНазначения = Результат.Форма.УникальныйИдентификатор;
			Сообщение.Текст = Результат.ВыводПредупреждения.Текст;
			Сообщение.Поле  = Результат.ВыводПредупреждения.ПутьКРеквизитуФормы;
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
	
	Если Не ОбработчикЗавершенияБудетВыполнен И Результат.ОбработчикЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(Результат.ОбработчикЗавершения, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

// Показывает окно предупреждение, а после его закрытия вызывает обработчик с заданным результатом.
Процедура ВернутьРезультатПослеПоказаПредупреждения(ТекстПредупреждения, Обработчик, Результат, Заголовок = Неопределено, Таймаут = 0) Экспорт
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("Обработчик", Обработчик);
	ПараметрыОбработчика.Вставить("Результат", Результат);
	Обработчик = Новый ОписаниеОповещения("ВернутьРезультатПослеЗакрытияПростогоДиалога", ЭтотОбъект, ПараметрыОбработчика);
	ПоказатьПредупреждение(Обработчик, ТекстПредупреждения, Таймаут, Заголовок);
КонецПроцедуры

// Продолжение процедуры (см. выше).
Процедура ВернутьРезультатПослеЗакрытияПростогоДиалога(ПараметрыОбработчика) Экспорт
	Если ТипЗнч(ПараметрыОбработчика.Обработчик) = Тип("ОписаниеОповещения") Тогда
		ВыполнитьОбработкуОповещения(ПараметрыОбработчика.Обработчик, ПараметрыОбработчика.Результат);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Табличный документ

// Формирует описание выделенных областей табличного документа, которое можно передавать на сервер.
//   Служит заменой типа ВыделенныеОбластиТабличногоДокумента
//   когда нужно вычислить сумму ячеек на сервере без контекста.
//
// Параметры:
//   ТабличныйДокумент - ТабличныйДокумент - Таблица, для которой нужно сформировать описание выделенных ячеек.
//
// Возвращаемое значение: 
//   Массив из Структура - описание.
//       * Верх  - Число - Номер строки верхней границы области.
//       * Низ   - Число - Номер строки нижней границы области.
//       * Лево  - Число - Номер колонки верхней границы области.
//       * Право - Число - Номер колонки нижней границы области.
//       * ТипОбласти - ТипОбластиЯчеекТабличногоДокумента - Колонки, Прямоугольник, Строки, Таблица.
//
// См. также:
//   СтандартныеПодсистемыКлиентСервер.СуммаЯчеек().
//   СтандартныеПодсистемыВызовСервера.СуммаЯчеек().
//
Функция ВыделенныеОбласти(ТабличныйДокумент) Экспорт
	Результат = Новый Массив;
	Для Каждого ВыделеннаяОбласть Из ТабличныйДокумент.ВыделенныеОбласти Цикл
		Если ТипЗнч(ВыделеннаяОбласть) <> Тип("ОбластьЯчеекТабличногоДокумента") Тогда
			Продолжить;
		КонецЕсли;
		Структура = Новый Структура("Верх, Низ, Лево, Право, ТипОбласти");
		ЗаполнитьЗначенияСвойств(Структура, ВыделеннаяОбласть);
		Результат.Добавить(Структура);
	КонецЦикла;
	Возврат Результат;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики условных вызовов в другие подсистемы.

// Открывает форму ввода пароля пользователя сервиса.
//
// Параметры:
//  ОбработкаПродолжения      - ОписаниеОповещения, которое нужно обработать после получения пароля.
//  ФормаВладелец             - Неопределено, УправляемаяФорма - форма, которая запрашивает пароль.
//  ПарольПользователяСервиса - Строка - текущий пароль пользователя сервиса.
//
Процедура ПриЗапросеПароляДляАутентификацииВСервисе(ОбработкаПродолжения, ФормаВладелец = Неопределено, ПарольПользователяСервиса = Неопределено) Экспорт
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаВМоделиСервиса.ПользователиВМоделиСервиса") Тогда
		
		МодульПользователиСлужебныйВМоделиСервисаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ПользователиСлужебныйВМоделиСервисаКлиент");
		
		МодульПользователиСлужебныйВМоделиСервисаКлиент.ЗапроситьПарольДляАутентификацииВСервисе(
			ОбработкаПродолжения, ФормаВладелец, ПарольПользователяСервиса);
	КонецЕсли;
	
КонецПроцедуры

// Переадресует оповещение без результата на оповещение с результатом.
Функция ОповещениеБезРезультата(ОповещениеСРезультатом) Экспорт
	
	Возврат Новый ОписаниеОповещения("ВыполнитьОповещениеСПустымРезультатом", ЭтотОбъект, ОповещениеСРезультатом);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// ПередНачаломРаботыСистемы

// Только для внутреннего использования. Продолжение процедуры ПередНачаломРаботыСистемы.
Процедура ДействияПередНачаломРаботыСистемыПослеПроверкиВерсииПлатформы(Неопределен, Параметры) Экспорт
	
	Если НЕ ПродолжитьДействияПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
		"ДействияПередНачаломРаботыСистемыПослеВосстановленияСвязиСГлавнымУзлом", ЭтотОбъект, Параметры));
	
	Попытка
		ПроверитьНеобходимостьВосстановленияСвязиСГлавнымУзлом(Параметры);
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Ложь);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПередНачаломРаботыСистемы.
Процедура ДействияПередНачаломРаботыСистемыПослеВосстановленияСвязиСГлавнымУзлом(Неопределен, Параметры) Экспорт
	
	Если НЕ ПродолжитьДействияПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаВМоделиСервиса") Тогда
		
		Параметры.Вставить("ОбработкаПродолжения", Параметры.ОбработкаЗавершения);
		Попытка
			МодульРаботаВМоделиСервисаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаВМоделиСервисаКлиент");
			МодульРаботаВМоделиСервисаКлиент.ПередНачаломРаботыСистемы(Параметры);
		Исключение
			ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
		КонецПопытки;
		Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
		"ДействияПередНачаломРаботыСистемыПослеПроверкиЛегальности", ЭтотОбъект, Параметры));
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует(
		   "СтандартныеПодсистемы.ПроверкаЛегальностиПолученияОбновления") Тогда
		
		Попытка
			МодульПроверкаЛегальностиПолученияОбновленияКлиент =
				ОбщегоНазначенияКлиент.ОбщийМодуль("ПроверкаЛегальностиПолученияОбновленияКлиент");
			
			МодульПроверкаЛегальностиПолученияОбновленияКлиент.ПроверитьЛегальностьПолученияОбновленияПриЗапуске(Параметры);
		Исключение
			ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
		КонецПопытки;
		Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Ложь);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПередНачаломРаботыСистемы.
Процедура ДействияПередНачаломРаботыСистемыПослеПроверкиЛегальности(Неопределен, Параметры) Экспорт
	
	Если НЕ ПродолжитьДействияПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
		"ДействияПередНачаломРаботыСистемыПослеПовторенияЗагрузкиСообщенияОбменаДанными", ЭтотОбъект, Параметры));
	
	Попытка
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ОбменДанными") Тогда
			// Запрос у пользователя с или без повтора загрузки сообщения обмена данными.
			МодульОбменДаннымиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменДаннымиКлиент");
			МодульОбменДаннымиКлиент.ПередНачаломРаботыСистемы(Параметры);
		КонецЕсли;
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Ложь);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПередНачаломРаботыСистемы.
Процедура ДействияПередНачаломРаботыСистемыПослеПовторенияЗагрузкиСообщенияОбменаДанными(Неопределен, Параметры) Экспорт
	
	Если НЕ ПродолжитьДействияПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	Если ПараметрыКлиента.ДоступноИспользованиеРазделенныхДанных Тогда
		
		Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
			"ДействияПередНачаломРаботыСистемыПослеОбновленияПараметровРаботыПрограммы", ЭтотОбъект, Параметры));
	Иначе
		Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
			"ДействияПередНачаломРаботыСистемыПослеПереопределяемойПроцедуры", ЭтотОбъект, Параметры));
	КонецЕсли;
	
	Попытка
		// - Остановка при блокировке информационной базы для обновления.
		// - Подготовка параметров работы программы.
		// - Обновление неразделенных данных.
		ОбновлениеИнформационнойБазыКлиент.ПередНачаломРаботыСистемы(Параметры);
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПередНачаломРаботыСистемы.
Процедура ДействияПередНачаломРаботыСистемыПослеОбновленияПараметровРаботыПрограммы(Неопределен, Параметры) Экспорт
	
	Если НЕ ПродолжитьДействияПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ОбработкаПродолжения", Параметры.ОбработкаЗавершения);
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует(
		"ТехнологияСервиса.РаботаВМоделиСервиса.ОбменДаннымиВМоделиСервиса") Тогда
		
		МодульАвтономнаяРаботаСлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("АвтономнаяРаботаСлужебныйКлиент");
		Попытка
			МодульАвтономнаяРаботаСлужебныйКлиент.ПередНачаломРаботыСистемы(Параметры);
		Исключение
			ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
		КонецПопытки;
		Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		ПользователиСлужебныйКлиент.ПередНачаломРаботыСистемы(Параметры);
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		УстановитьРасширенныйЗаголовокПриложения(Истина); // Для вспомогательных окон.
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
		"ДействияПередНачаломРаботыСистемыПослеОбработкиВходаСРазблокирующимКодом", ЭтотОбъект, Параметры));
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ЗавершениеРаботыПользователей") Тогда
		
		Попытка
			МодульСоединенияИБКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СоединенияИБКлиент");
			МодульСоединенияИБКлиент.ПередНачаломРаботыСистемы(Параметры);
		Исключение
			ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
		КонецПопытки;
		Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПередНачаломРаботыСистемы.
Процедура ДействияПередНачаломРаботыСистемыПослеОбработкиВходаСРазблокирующимКодом(Неопределен, Параметры) Экспорт
	
	Если НЕ ПродолжитьДействияПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("НомерСледующегоОбработчика", 1);
	Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
		"ДействияПередНачаломРаботыСистемыВОбработчикахСлужебногоСобытия", ЭтотОбъект, Параметры));
	
	Попытка
		ОбновлениеИнформационнойБазыКлиент.ОбновитьИнформационнуюБазу(Параметры);
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПередНачаломРаботыСистемы.
Процедура ДействияПередНачаломРаботыСистемыВОбработчикахСлужебногоСобытия(Неопределен, Параметры) Экспорт
	
	Если НЕ ПродолжитьДействияПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработчикиСобытия = ОбщегоНазначенияКлиент.ОбработчикиСлужебногоСобытия(
		"СтандартныеПодсистемы.БазоваяФункциональность\ПередНачаломРаботыСистемы");
	
	КоличествоОбработчиков = ОбработчикиСобытия.Количество();
	НачальныйНомер = Параметры.НомерСледующегоОбработчика;
	
	Для Номер = НачальныйНомер По КоличествоОбработчиков Цикл
		Параметры.ИнтерактивнаяОбработка = Неопределено;
		Параметры.НомерСледующегоОбработчика = Номер + 1;
		Обработчик = ОбработчикиСобытия.Получить(Номер - 1);
		
		Попытка
			Обработчик.Модуль.ПередНачаломРаботыСистемы(Параметры);
		Исключение
			ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск");
		КонецПопытки;
		Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
		"ДействияПередНачаломРаботыСистемыПослеПереопределяемойПроцедуры", ЭтотОбъект, Параметры));
	
	Параметры.ИнтерактивнаяОбработка = Неопределено;
		
	Попытка
		ОбщегоНазначенияКлиентПереопределяемый.ПередНачаломРаботыСистемы(Параметры);
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск");
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПередНачаломРаботыСистемы.
Процедура ДействияПередНачаломРаботыСистемыПослеПереопределяемойПроцедуры(Неопределен, Параметры) Экспорт
	
	Если НЕ ПродолжитьДействияПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ОбработкаПродолжения", Параметры.ОбработкаЗавершения);
	
	Попытка
		УстановитьПараметрыФункциональныхОпцийИнтерфейсаПриЗапуске();
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск", Истина);
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Только для внутреннего использования. Завершение процедуры ПередНачаломРаботыСистемы.
Процедура ДействияПередНачаломРаботыСистемыОбработкаЗавершения(Неопределен, Параметры) Экспорт
	
	ПараметрыПриЗапускеИЗавершенииПрограммы = ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы"];
	ПараметрыПриЗапускеИЗавершенииПрограммы.Удалить("ПолученныеПараметрыКлиента");
	
	Если Параметры.ОповещениеЗавершения <> Неопределено Тогда
		Результат = Новый Структура;
		Результат.Вставить("Отказ", Параметры.Отказ);
		Результат.Вставить("Перезапустить", Параметры.Перезапустить);
		Результат.Вставить("ДополнительныеПараметрыКоманднойСтроки", Параметры.ДополнительныеПараметрыКоманднойСтроки);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеЗавершения, Результат);
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отказ Тогда
		Если Параметры.Перезапустить <> Истина Тогда
			ПрекратитьРаботуСистемы();
		ИначеЕсли ЗначениеЗаполнено(Параметры.ДополнительныеПараметрыКоманднойСтроки) Тогда
			ПрекратитьРаботуСистемы(Параметры.Перезапустить, Параметры.ДополнительныеПараметрыКоманднойСтроки);
		Иначе
			ПрекратитьРаботуСистемы(Параметры.Перезапустить);
		КонецЕсли;
		
	ИначеЕсли НЕ Параметры.НепрерывноеВыполнение Тогда
		Если ПараметрыПриЗапускеИЗавершенииПрограммы.Свойство("ПараметрыОбработки") Тогда
			ПараметрыПриЗапускеИЗавершенииПрограммы.Удалить("ПараметрыОбработки");
		КонецЕсли;
		ПодключитьОбработчикОжидания("ОбработчикОжиданияПриНачалеРаботыСистемы", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПриНачалеРаботыСистемы

// Только для внутреннего использования. Продолжение процедуры ПриНачалеРаботыСистемы.
Процедура ДействияПриНачалеРаботыСистемыВОбработчикахСлужебногоСобытия(Неопределен, Параметры) Экспорт

	Если Параметры.Отказ Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения);
		Возврат;
	КонецЕсли;
	
	ОбработчикиСобытия = ОбщегоНазначенияКлиент.ОбработчикиСлужебногоСобытия(
		"СтандартныеПодсистемы.БазоваяФункциональность\ПриНачалеРаботыСистемы");
	
	КоличествоОбработчиков = ОбработчикиСобытия.Количество();
	НачальныйНомер = Параметры.НомерСледующегоОбработчика;
	
	Для Номер = НачальныйНомер По КоличествоОбработчиков Цикл
		Параметры.НомерСледующегоОбработчика = Номер + 1;
		Обработчик = ОбработчикиСобытия.Получить(Номер - 1);
		
		Попытка
			Обработчик.Модуль.ПриНачалеРаботыСистемы(Параметры);
		Исключение
			ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск");
		КонецПопытки;
		Если ИнтерактивнаяОбработкаПриНачалеРаботыСистемы(Параметры) Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	// Подготовка перехода к следующей процедуре.
	Параметры.Вставить("НомерСледующегоОбработчика", 1);
	Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
		"ДействияПослеНачалаРаботыСистемыВОбработчикахСлужебногоСобытия", ЭтотОбъект, Параметры));
	
	Попытка
		ОбщегоНазначенияКлиентПереопределяемый.ПриНачалеРаботыСистемы(Параметры);
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск");
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПриНачалеРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПриНачалеРаботыСистемы.
Процедура ДействияПослеНачалаРаботыСистемыВОбработчикахСлужебногоСобытия(Неопределен, Параметры) Экспорт

	Если Параметры.Отказ Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения);
		Возврат;
	КонецЕсли;
	
	ОбработчикиСобытия = ОбщегоНазначенияКлиент.ОбработчикиСлужебногоСобытия(
		"СтандартныеПодсистемы.БазоваяФункциональность\ПослеНачалаРаботыСистемы");
	
	КоличествоОбработчиков = ОбработчикиСобытия.Количество();
	НачальныйНомер = Параметры.НомерСледующегоОбработчика;
	
	Для Номер = НачальныйНомер По КоличествоОбработчиков Цикл
		Параметры.НомерСледующегоОбработчика = Номер + 1;
		Обработчик = ОбработчикиСобытия.Получить(Номер - 1);
		
		Попытка
			Обработчик.Модуль.ПослеНачалаРаботыСистемы();
		Исключение
			ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск");
		КонецПопытки;
		Если ИнтерактивнаяОбработкаПриНачалеРаботыСистемы(Параметры) Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	Параметры.Вставить("ОбработкаПродолжения", Параметры.ОбработкаЗавершения);
	Попытка
		ОбщегоНазначенияКлиентПереопределяемый.ПослеНачалаРаботыСистемы();
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Запуск");
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПриНачалеРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Только для внутреннего использования. Завершение процедуры ПриНачалеРаботыСистемы.
Процедура ДействияПриНачалеРаботыСистемыОбработкаЗавершения(Неопределен, Параметры) Экспорт
	
	Если НЕ Параметры.Отказ Тогда
		ПараметрыПриЗапускеИЗавершенииПрограммы = ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы"];
		Если ПараметрыПриЗапускеИЗавершенииПрограммы.Свойство("ПропуститьОчисткуСкрытияРабочегоСтола") Тогда
			ПараметрыПриЗапускеИЗавершенииПрограммы.Удалить("ПропуститьОчисткуСкрытияРабочегоСтола");
		КонецЕсли;
		СкрытьРабочийСтолПриНачалеРаботыСистемы(Ложь);
	КонецЕсли;
	
	Если Параметры.ОповещениеЗавершения <> Неопределено Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("Отказ", Параметры.Отказ);
		Результат.Вставить("Перезапустить", Параметры.Перезапустить);
		Результат.Вставить("ДополнительныеПараметрыКоманднойСтроки", Параметры.ДополнительныеПараметрыКоманднойСтроки);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеЗавершения, Результат);
		Возврат;
		
	Иначе
		Если Параметры.Отказ Тогда
			Если Параметры.Перезапустить <> Истина Тогда
				ПрекратитьРаботуСистемы();
				
			ИначеЕсли ЗначениеЗаполнено(Параметры.ДополнительныеПараметрыКоманднойСтроки) Тогда
				ПрекратитьРаботуСистемы(Параметры.Перезапустить, Параметры.ДополнительныеПараметрыКоманднойСтроки);
			Иначе
				ПрекратитьРаботуСистемы(Параметры.Перезапустить);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Обработать параметры запуска программы.
//
// Возвращаемое значение:
//   Булево   - Истина, если необходимо прервать выполнение процедуры ПриНачалеРаботыСистемы.
//
Функция ОбработатьПараметрыЗапуска()

	Если ПустаяСтрока(ПараметрЗапуска) Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Параметр может состоять из частей, разделенных символом ";".
	// Первая часть - главное значение параметра запуска. 
	// Наличие дополнительных частей определяется логикой обработки главного параметра.
	ПараметрыЗапуска = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПараметрЗапуска, ";");
	ПервыйПараметр = ВРег(ПараметрыЗапуска[0]);
	
	Отказ = Ложь;
	ОбработчикиСобытия = ОбщегоНазначенияКлиент.ОбработчикиСлужебногоСобытия(
		"СтандартныеПодсистемы.БазоваяФункциональность\ПриОбработкеПараметровЗапуска");
	
	Для каждого Обработчик Из ОбработчикиСобытия Цикл
		Обработчик.Модуль.ПриОбработкеПараметровЗапуска(ПервыйПараметр, ПараметрыЗапуска, Отказ);
	КонецЦикла;
	
	Отказ = ОбщегоНазначенияКлиентПереопределяемый.ОбработатьПараметрыЗапуска(
		ПервыйПараметр, ПараметрыЗапуска) Или Отказ;
	
	Если Не Отказ Тогда
		ОбщегоНазначенияКлиентПереопределяемый.ПриОбработкеПараметровЗапуска(ПервыйПараметр, ПараметрыЗапуска, Отказ);
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПередЗавершениемРаботыСистемы

// Только для внутреннего использования. Продолжение процедуры ПередЗавершениемРаботыСистемы.
Процедура ДействияПередЗавершениемРаботыСистемыВОбработчикахСлужебногоСобытия(Неопределен, Параметры) Экспорт

	Если Параметры.Отказ Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения);
		Возврат;
	КонецЕсли;
	
	ОбработчикиСобытия = ОбщегоНазначенияКлиент.ОбработчикиСлужебногоСобытия(
		"СтандартныеПодсистемы.БазоваяФункциональность\ПередЗавершениемРаботыСистемы");
	
	КоличествоОбработчиков = ОбработчикиСобытия.Количество();
	НачальныйНомер = Параметры.НомерСледующегоОбработчика;
	
	Для Номер = НачальныйНомер По КоличествоОбработчиков Цикл
		Параметры.НомерСледующегоОбработчика = Номер + 1;
		Обработчик = ОбработчикиСобытия.Получить(Номер - 1);
		Попытка
			Обработчик.Модуль.ПередЗавершениемРаботыСистемы(Параметры);
		Исключение
			ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Завершение");
		КонецПопытки;
		Если ИнтерактивнаяОбработкаПередЗавершениемРаботыСистемы(Параметры) Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	Параметры.Вставить("ОбработкаПродолжения", Новый ОписаниеОповещения(
		"ДействияПередЗавершениемРаботыСистемыПослеОбработчиковСлужебногоСобытия", ЭтотОбъект, Параметры));
	
	Попытка
		ОбщегоНазначенияКлиентПереопределяемый.ПередЗавершениемРаботыСистемы(Параметры);
	Исключение
		ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Завершение");
	КонецПопытки;
	Если ИнтерактивнаяОбработкаПередЗавершениемРаботыСистемы(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПередЗавершениемРаботыСистемы.
Процедура ДействияПередЗавершениемРаботыСистемыПослеОбработчиковСлужебногоСобытия(Неопределен, Параметры) Экспорт
	
	Если Параметры.Отказ Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения);
		Возврат;
	КонецЕсли;
	
	// Если перед завершение работы есть незаписанные сообщения для журнала регистрации,
	// накопленные в переменной, то их необходимо записать.
	ИмяПараметра = "СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, Новый СписокЗначений);
	КонецЕсли;
	
	Если ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"].Количество() <> 0 Тогда
		ЖурналРегистрацииВызовСервера.ЗаписатьСобытияВЖурналРегистрации(ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"]);
	КонецЕсли;
	
	Параметры.Вставить("ОбработкаПродолжения", Параметры.ОбработкаЗавершения);
	Если СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ДоступноИспользованиеРазделенныхДанных Тогда
		Попытка
			ОткрытьФормуПредупрежденийПриЗавершенииРаботы(Параметры);
		Исключение
			ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке(), "Завершение");
		КонецПопытки;
		Если ИнтерактивнаяОбработкаПередЗавершениемРаботыСистемы(Параметры) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Только для внутреннего использования. Завершение процедуры ПередЗавершениемРаботыСистемы.
Процедура ДействияПередЗавершениемРаботыСистемыОбработкаЗавершения(Неопределен, Параметры) Экспорт
	
	Если Параметры.ОповещениеЗавершения <> Неопределено Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("Отказ", Параметры.Отказ);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеЗавершения, Результат);
		
	ИначеЕсли НЕ Параметры.Отказ И НЕ Параметры.НепрерывноеВыполнение Тогда
		
		ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы"].Вставить("ДействияПередЗавершениемРаботыСистемыВыполнены");
		ЗавершитьРаботуСистемы();
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования. Завершение процедуры ПередЗавершениемРаботыСистемы.
Процедура ДействияПередЗавершениемРаботыСистемыПослеОбработкиОшибки(Неопределен, ДополнительныеПараметры) Экспорт
	
	Параметры = ДополнительныеПараметры.Параметры;
	Параметры.ОбработкаПродолжения = ДополнительныеПараметры.ОбработкаПродолжения;
	
	Если Параметры.Отказ Тогда
		Параметры.Отказ = Ложь;
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения);
	Иначе
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочие процедуры и функции для запуска и завершения работы программы.

// Проверяет минимально допустимую версию платформы для запуска.
// Если версия платформы более поздняя, чем РекомендуемаяВерсияПлатформы, то пользователю будет 
// показано оповещение. Работа программы будет прекращена, если ЗавершитьРаботу = Истина.
//
// Возвращаемое значение
//  Булево - если версия актуальна, тогда Истина, иначе - Ложь.
//
Процедура ПроверитьВерсиюПлатформыПриЗапуске(Параметры)
	
	ПараметрыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	
	Если НЕ ПараметрыКлиента.Свойство("ПоказатьНерекомендуемуюВерсиюПлатформы") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
		"ИнтерактивнаяОбработкаПроверкиВерсииПлатформыПриЗапуске", ЭтотОбъект, Параметры);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПроверитьВерсиюПлатформыПриЗапуске.
Процедура ИнтерактивнаяОбработкаПроверкиВерсииПлатформыПриЗапуске(Параметры, Неопределен) Экспорт
	
	ПараметрыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	
	Если ПараметрыКлиента.РаботаВПрограммеЗапрещена Тогда
		Если ПараметрыКлиента.ЕстьДоступДляОбновленияВерсииПлатформы Тогда
			ТекстСообщения = НСтр("ru = 'Вход в программу невозможен.
				|Необходимо предварительно обновить версию платформы 1С:Предприятие.'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Вход в программу невозможен.
				|Необходимо обратиться к администратору для обновления версии платформы 1С:Предприятие.'");
		КонецЕсли;
	Иначе
		Если ПараметрыКлиента.ЕстьДоступДляОбновленияВерсииПлатформы Тогда
			ТекстСообщения = 
				НСтр("ru='Рекомендуется завершить работу программы и обновить версию платформы 1С:Предприятия.
			         |В противном случае некоторые возможности программы будут недоступны или будут работать некорректно.'");
		Иначе
			ТекстСообщения = 
				НСтр("ru='Рекомендуется завершить работу программы и обратиться к администратору для обновления версии платформы 1С:Предприятия.
			         |В противном случае некоторые возможности программы будут недоступны или будут работать некорректно.'");
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекстСообщения", ТекстСообщения);
	ПараметрыФормы.Вставить("ЗавершитьРаботу", ПараметрыКлиента.РаботаВПрограммеЗапрещена);
	ПараметрыФормы.Вставить("РекомендуемаяВерсияПлатформы", ПараметрыКлиента.МинимальноНеобходимаяВерсияПлатформы);
	ПараметрыФормы.Вставить("ОткрытаПоСценарию", Истина);
	ПараметрыФормы.Вставить("ПропуститьЗавершениеРаботы", Истина);
	
	Форма = ОткрытьФорму("Обработка.НерекомендуемаяВерсияПлатформы.Форма.НерекомендуемаяВерсияПлатформы", ПараметрыФормы,
		, , , , Новый ОписаниеОповещения("ПослеЗакрытияФормыНерекомендуемойВерсииПлатформы", ЭтотОбъект, Параметры));
	
	Если Форма = Неопределено Тогда
		ПослеЗакрытияФормыНерекомендуемойВерсииПлатформы("Продолжить", Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПроверитьВерсиюПлатформыПриЗапуске.
Процедура ПослеЗакрытияФормыНерекомендуемойВерсииПлатформы(Результат, Параметры) Экспорт
	
	Если Результат <> "Продолжить" Тогда
		Параметры.Отказ = Истина;
	Иначе
		Параметры.ПолученныеПараметрыКлиента.Вставить("ПоказатьНерекомендуемуюВерсиюПлатформы");
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Проверяет необходимость восстановления связи с главным узлом и начинает восстановление, если требуется.
Процедура ПроверитьНеобходимостьВосстановленияСвязиСГлавнымУзлом(Параметры)
	
	ПараметрыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	
	Если НЕ ПараметрыКлиента.Свойство("ВосстановитьСвязьСГлавнымУзлом") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
		"ИнтерактивнаяОбработкаВосстановленияСвязиСГлавнымУзлом", ЭтотОбъект, Параметры);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПроверитьНеобходимостьВосстановленияСвязиСГлавнымУзлом.
Процедура ИнтерактивнаяОбработкаВосстановленияСвязиСГлавнымУзлом(Параметры, Неопределен) Экспорт
	
	ПараметрыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	
	Если ПараметрыКлиента.ВосстановитьСвязьСГлавнымУзлом = Ложь Тогда
		Параметры.Отказ = Истина;
		ПоказатьПредупреждение(
			ОповещениеБезРезультата(Параметры.ОбработкаПродолжения),
			НСтр("ru = 'Вход в программу временно невозможен до восстановления связи с главным узлом.
			           |Обратитесь к администратору за подробностями.'"),
			15);
		Возврат;
	КонецЕсли;
	
	Форма = ОткрытьФорму("ОбщаяФорма.ВосстановлениеСвязиСГлавнымУзлом",,,,,,
		Новый ОписаниеОповещения("ПослеЗакрытияФормыВосстановленияСвязиСГлавнымУзлом", ЭтотОбъект, Параметры));
	
	Если Форма = Неопределено Тогда
		ПослеЗакрытияФормыВосстановленияСвязиСГлавнымУзлом(Новый Структура("Отказ", Истина), Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры ПроверитьНеобходимостьВосстановленияСвязиСГлавнымУзлом.
Процедура ПослеЗакрытияФормыВосстановленияСвязиСГлавнымУзлом(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Параметры.Отказ = Истина;
		
	ИначеЕсли Результат.Отказ Тогда
		Параметры.Отказ = Истина;
	Иначе
		Параметры.ПолученныеПараметрыКлиента.Вставить("ВосстановитьСвязьСГлавнымУзлом");
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Вызывается при необходимости открыть форму списка активных пользователей,
// которые в данный момент времени работают с системой.
//
Процедура ОткрытьСписокАктивныхПользователей(ПараметрыФормы = Неопределено) Экспорт
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ЗавершениеРаботыПользователей") Тогда
		
		ИмяФормы = "";
		МодульСоединенияИБКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СоединенияИБКлиент");
		МодульСоединенияИБКлиент.ПриОпределенииФормыАктивныхПользователей(ИмяФормы);
		ОткрытьФорму(ИмяФормы, ПараметрыФормы);
		
	Иначе
		
		ПоказатьПредупреждение(,
			НСтр("ru = 'Для того чтобы открыть список активных пользователей, перейдите в меню
				       |Все функции - Стандартные - Активные пользователи.'"));
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает флаг скрытия рабочего стола при начале работы системы,
// который блокирует создание форм на рабочем столе.
// Снимает флаг скрытия и обновляет рабочий стол, когда это станет возможным,
// если скрытие выполнялось.
//
// Параметры:
//  Скрыть - Булево. Если передать Ложь, тогда при условии скрытия рабочего
//           стола он будет вновь показан.
//
//  УжеВыполненоНаСервере - Булево. Если передать Истина, тогда уже был вызван
//           метод в модуле СтандартныеПодсистемыВызовСервера, и его не требуется
//           вызвать, а требуется только установить на клиенте, что рабочий стол
//           был скрыт и позднее его требуется показать.
//
Процедура СкрытьРабочийСтолПриНачалеРаботыСистемы(Скрыть = Истина, УжеВыполненоНаСервере = Ложь) Экспорт
	
	ПараметрыПриЗапускеИЗавершенииПрограммы = ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы"];
	
	Если Скрыть Тогда
		Если НЕ ПараметрыПриЗапускеИЗавершенииПрограммы.Свойство("СкрытьРабочийСтолПриНачалеРаботыСистемы") Тогда
			ПараметрыПриЗапускеИЗавершенииПрограммы.Вставить("СкрытьРабочийСтолПриНачалеРаботыСистемы");
			Если НЕ УжеВыполненоНаСервере Тогда
				СтандартныеПодсистемыВызовСервера.СкрытьРабочийСтолПриНачалеРаботыСистемы();
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ПараметрыПриЗапускеИЗавершенииПрограммы.Свойство("СкрытьРабочийСтолПриНачалеРаботыСистемы") Тогда
			ПараметрыПриЗапускеИЗавершенииПрограммы.Удалить("СкрытьРабочийСтолПриНачалеРаботыСистемы");
			Если НЕ УжеВыполненоНаСервере Тогда
				СтандартныеПодсистемыВызовСервера.СкрытьРабочийСтолПриНачалеРаботыСистемы(Ложь);
			КонецЕсли;
			ТекущееАктивноеОкно = АктивноеОкно();
			ОбновитьИнтерфейс();
			Если ТекущееАктивноеОкно <> Неопределено Тогда
				ТекущееАктивноеОкно.Активизировать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования.
Процедура ВыполнитьОповещениеСПустымРезультатом(ОповещениеСРезультатом) Экспорт
	
	ВыполнитьОбработкуОповещения(ОповещениеСРезультатом);
	
КонецПроцедуры

// Только для внутреннего использования.
Процедура НачатьИнтерактивнуюОбработкуПередЗавершениемРаботыСистемы() Экспорт
	
	ПараметрыПриЗапускеИЗавершенииПрограммы = ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы"];
	Если НЕ ПараметрыПриЗапускеИЗавершенииПрограммы.Свойство("ПараметрыОбработкиЗавершения") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПараметрыПриЗапускеИЗавершенииПрограммы.ПараметрыОбработкиЗавершения;
	ПараметрыПриЗапускеИЗавершенииПрограммы.Удалить("ПараметрыОбработкиЗавершения");
	
	ИнтерактивнаяОбработка = Параметры.ИнтерактивнаяОбработка;
	Параметры.ИнтерактивнаяОбработка = Неопределено;
	ВыполнитьОбработкуОповещения(ИнтерактивнаяОбработка, Параметры);
	
КонецПроцедуры

// Только для внутреннего использования.
Процедура ПослеЗакрытияФормыПредупрежденийПриЗавершенииРаботы(Результат, ДополнительныеПараметры) Экспорт
	
	Параметры = ДополнительныеПараметры.Параметры;
	
	Если ДополнительныеПараметры.ВариантФормы = "Вопрос" Тогда
		
		Если Результат <> Неопределено И Результат.БольшеНеЗадаватьЭтотВопрос Тогда
			СтандартныеПодсистемыВызовСервера.СохранитьНастройкуПодтвержденияПриЗавершенииПрограммы(
				НЕ Результат.БольшеНеЗадаватьЭтотВопрос);
		КонецЕсли;
		
		Если Результат = Неопределено Или Результат.Значение <> КодВозвратаДиалога.Да Тогда
			Параметры.Отказ = Истина;
		КонецЕсли;
		
	ИначеЕсли ДополнительныеПараметры.ВариантФормы = "СтандартнаяФорма" Тогда
	
		Если Результат = Истина Или Результат = Неопределено Тогда
			Параметры.Отказ = Истина;
		КонецЕсли;
		
	Иначе // ПрикладнаяФорма
		Если Результат = Истина Или Результат = Неопределено Или Результат = КодВозвратаДиалога.Нет Тогда
			Параметры.Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

Процедура ПослеНачалаРаботыСистемы() Экспорт
	
	Если ТребуетсяПоказРекомендацииПоОбъемуОперативнойПамяти() Тогда
		ПодключитьОбработчикОжидания("ПоказатьРекомендациюПоОбъемуОперативнойПамяти", 10, Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции.

// Возвращает строковое представление значения типа КодВозвратаДиалога.
Функция КодВозвратаДиалогаВСтроку(Значение)
	
	Результат = "КодВозвратаДиалога." + Строка(Значение);
	
	Если Значение = КодВозвратаДиалога.Да Тогда
		Результат = "КодВозвратаДиалога.Да";
	ИначеЕсли Значение = КодВозвратаДиалога.Нет Тогда
		Результат = "КодВозвратаДиалога.Нет";
	ИначеЕсли Значение = КодВозвратаДиалога.ОК Тогда
		Результат = "КодВозвратаДиалога.ОК";
	ИначеЕсли Значение = КодВозвратаДиалога.Отмена Тогда
		Результат = "КодВозвратаДиалога.Отмена";
	ИначеЕсли Значение = КодВозвратаДиалога.Повторить Тогда
		Результат = "КодВозвратаДиалога.Повторить";
	ИначеЕсли Значение = КодВозвратаДиалога.Прервать Тогда
		Результат = "КодВозвратаДиалога.Прервать";
	ИначеЕсли Значение = КодВозвратаДиалога.Пропустить Тогда
		Результат = "КодВозвратаДиалога.Пропустить";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Установить разделения сеанса при запуске программы.
Процедура УстановитьРазделениеСеанса()

	Если ПустаяСтрока(ПараметрЗапуска) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапуска = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПараметрЗапуска, ";");
	ЗначениеПараметраЗапуска = ВРег(ПараметрыЗапуска[0]);
	
	Если ЗначениеПараметраЗапуска <> ВРег("ВойтиВОбластьДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыЗапуска.Количество() < 2 Тогда
		ВызватьИсключение
			НСтр("ru = 'При указании параметра запуска ВойтиВОбластьДанных,
			           |дополнительным параметром необходимо указать значение разделителя.'");
	КонецЕсли;
	
	Попытка
		ЗначениеРазделителя = Число(ПараметрыЗапуска[1]);
	Исключение
		ВызватьИсключение
			НСтр("ru = 'Значением разделителя в параметре ВойтиВОбластьДанных должно быть число.'");
	КонецПопытки;
	
	ОбщегоНазначенияВызовСервера.УстановитьРазделениеСеанса(Истина, ЗначениеРазделителя);
	
КонецПроцедуры 

// В случае отказа вызывает обработку завершения,
// если добавлен новый полученный параметр клиента,
// обновляет параметры работы клиента.
//
Функция ПродолжитьДействияПередНачаломРаботыСистемы(Параметры)
	
	Если Параметры.Отказ Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения);
		Возврат Ложь;
	КонецЕсли;
	
	ОбновитьПараметрыРаботыКлиента(Параметры);
	
	Возврат Истина;
	
КонецФункции

// Обновляет параметры работы клиента после очередной интерактивной обработки при запуске.
Процедура ОбновитьПараметрыРаботыКлиента(Параметры, ПервыйВызов = Ложь, ОбновитьПовторноИспользуемыеЗначения = Истина)
	
	Если ПервыйВызов Тогда
		ИмяПараметра = "СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы";
		Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
			ПараметрыПриложения.Вставить(ИмяПараметра, Новый Структура);
		КонецЕсли;
	ИначеЕсли Параметры.КоличествоПолученныхПараметровКлиента = Параметры.ПолученныеПараметрыКлиента.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("КоличествоПолученныхПараметровКлиента", Параметры.ПолученныеПараметрыКлиента.Количество());
	
	ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы"].Вставить(
		"ПолученныеПараметрыКлиента", Параметры.ПолученныеПараметрыКлиента);
	
	Если ОбновитьПовторноИспользуемыеЗначения Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
КонецПроцедуры

Функция ВыполняетсяИнтерактивнаяОбработкаПередНачаломРаботыСистемы()
	
	ПараметрыПриЗапускеИЗавершенииПрограммы = ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы"];
	
	Если НЕ ПараметрыПриЗапускеИЗавершенииПрограммы.Свойство("ПараметрыОбработки") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Параметры = ПараметрыПриЗапускеИЗавершенииПрограммы.ПараметрыОбработки;
	
	Если Параметры.ИнтерактивнаяОбработка <> Неопределено Тогда
		Параметры.НепрерывноеВыполнение = Ложь;
		ИнтерактивнаяОбработка = Параметры.ИнтерактивнаяОбработка;
		Параметры.ИнтерактивнаяОбработка = Неопределено;
		ВыполнитьОбработкуОповещения(ИнтерактивнаяОбработка, Параметры);
		ПараметрыПриЗапускеИЗавершенииПрограммы.Удалить("ПараметрыОбработки");
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ИнтерактивнаяОбработкаПередНачаломРаботыСистемы(Параметры)
	
	ПараметрыПриЗапускеИЗавершенииПрограммы = ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы"];
	
	Если Параметры.ИнтерактивнаяОбработка = Неопределено Тогда
		Если Параметры.Отказ Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения);
			Возврат Истина;
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	ОбновитьПараметрыРаботыКлиента(Параметры);
	
	Если НЕ Параметры.НепрерывноеВыполнение Тогда
		ИнтерактивнаяОбработка = Параметры.ИнтерактивнаяОбработка;
		Параметры.ИнтерактивнаяОбработка = Неопределено;
		ВыполнитьОбработкуОповещения(ИнтерактивнаяОбработка, Параметры);
		
	ИначеЕсли Параметры.ОповещениеЗавершения = Неопределено Тогда
		// Выполнен вызов из обработчика события ПередНачаломРаботыСистемы для подготовки
		// выполнения интерактивной обработки в обработчике события ПриНачалеРаботыСистемы.
		
		ПараметрыПриЗапускеИЗавершенииПрограммы.Вставить("ПараметрыОбработки", Параметры);
		СкрытьРабочийСтолПриНачалеРаботыСистемы();
		ПараметрыПриЗапускеИЗавершенииПрограммы.Вставить("ПропуститьОчисткуСкрытияРабочегоСтола");
		УстановитьПараметрыФункциональныхОпцийИнтерфейсаПриЗапуске();
	Иначе
		// Выполнен вызов из процедуры ПередНачаломРаботыСистемы для немедленного
		// начала интерактивной обработки, т.к. процедура была вызвана программно, а не из
		// обработчика события ПередНачаломРаботыСистемы.
		Если ПараметрыПриЗапускеИЗавершенииПрограммы.Свойство("ПараметрыОбработки") Тогда
			ПараметрыПриЗапускеИЗавершенииПрограммы.Удалить("ПараметрыОбработки");
		КонецЕсли;
		
		Параметры.НепрерывноеВыполнение = Ложь;
		ИнтерактивнаяОбработка = Параметры.ИнтерактивнаяОбработка;
		Параметры.ИнтерактивнаяОбработка = Неопределено;
		ВыполнитьОбработкуОповещения(ИнтерактивнаяОбработка, Параметры);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ИнтерактивнаяОбработкаПриНачалеРаботыСистемы(Параметры)
	
	Если Параметры.ИнтерактивнаяОбработка = Неопределено Тогда
		Если Параметры.Отказ Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения);
			Возврат Истина;
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	ИнтерактивнаяОбработка = Параметры.ИнтерактивнаяОбработка;
	
	Параметры.НепрерывноеВыполнение = Ложь;
	Параметры.ИнтерактивнаяОбработка = Неопределено;
	
	ВыполнитьОбработкуОповещения(ИнтерактивнаяОбработка, Параметры);
	
	Возврат Истина;
	
КонецФункции

Функция ИнтерактивнаяОбработкаПередЗавершениемРаботыСистемы(Параметры)
	
	Если Параметры.ИнтерактивнаяОбработка = Неопределено Тогда
		Если Параметры.Отказ Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения);
			Возврат Истина;
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ Параметры.НепрерывноеВыполнение Тогда
		ИнтерактивнаяОбработка = Параметры.ИнтерактивнаяОбработка;
		Параметры.ИнтерактивнаяОбработка = Неопределено;
		ВыполнитьОбработкуОповещения(ИнтерактивнаяОбработка, Параметры);
		
	ИначеЕсли Параметры.ОповещениеЗавершения = Неопределено Тогда
		// Выполнен вызов из обработчика события ПередЗавершениемРаботыСистемы для подготовки
		// выполнения интерактивной обработки через обработчик ожидания.
		
		ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеИЗавершенииПрограммы"].Вставить("ПараметрыОбработкиЗавершения", Параметры);
		Параметры.НепрерывноеВыполнение = Ложь;
		ПодключитьОбработчикОжидания(
			"ОбработчикОжиданияИнтерактивнаяОбработкаПередЗавершениемРаботыСистемы", 0.1, Истина);
	Иначе
		// Выполнен вызов из процедуры ПередЗавершениемРаботыСистемы для немедленного
		// начала интерактивной обработки, т.к. процедура была вызвана программно, а не из
		// обработчика события ПередЗавершениемРаботыСистемы.
		
		Параметры.НепрерывноеВыполнение = Ложь;
		ИнтерактивнаяОбработка = Параметры.ИнтерактивнаяОбработка;
		Параметры.ИнтерактивнаяОбработка = Неопределено;
		ВыполнитьОбработкуОповещения(ИнтерактивнаяОбработка, Параметры);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Выводит форму сообщений пользователю при закрытии программы, либо выводит сообщение.
Процедура ОткрытьФормуПредупрежденийПриЗавершенииРаботы(Параметры)
	
	// В режимах работы веб-клиент и толстый клиент (обычное приложение) не выводится список предупреждений.
#Если ВебКлиент Или ТолстыйКлиентОбычноеПриложение Тогда
	Возврат;
#КонецЕсли

	Если ПараметрыПриложения["СтандартныеПодсистемы.ПропуститьПредупреждениеПередЗавершениемРаботыСистемы"] = Истина Тогда 
		Возврат;
	КонецЕсли;
	
	Предупреждения = Новый Массив;
	
	ОбработчикиСобытия = ОбщегоНазначенияКлиент.ОбработчикиСлужебногоСобытия(
		"СтандартныеПодсистемы.БазоваяФункциональность\ПриПолученииСпискаПредупрежденийЗавершенияРаботы");
	
	Для Каждого Обработчик Из ОбработчикиСобытия Цикл
		Обработчик.Модуль.ПриПолученииСпискаПредупрежденийЗавершенияРаботы(Предупреждения);
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Параметры", Параметры);
	ДополнительныеПараметры.Вставить("ВариантФормы", "Вопрос");
	
	ОбработкаОтвета = Новый ОписаниеОповещения("ПослеЗакрытияФормыПредупрежденийПриЗавершенииРаботы",
		ЭтотОбъект, ДополнительныеПараметры);
	
	Если Предупреждения.Количество() = 0 Тогда
		
		БольшеНеЗадаватьЭтотВопрос =
			Не СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента(
				).ЗапрашиватьПодтверждениеПриЗавершенииПрограммы;
		
		Если БольшеНеЗадаватьЭтотВопрос Тогда
			Возврат;
		КонецЕсли;
		Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
			"ВопросПользователюПередЗавершениемРаботыСистемы", ЭтотОбъект, ОбработкаОтвета);
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Предупреждения", Предупреждения);
		
		ИмяФормы = "ОбщаяФорма.ПредупрежденияПриЗавершенииРаботы";
		
		Если Предупреждения.Количество() = 1 Тогда
			Если Не ПустаяСтрока(Предупреждения[0].ТекстФлажка) Тогда 
				ДополнительныеПараметры.Вставить("ВариантФормы", "СтандартнаяФорма");
				ПараметрыОткрытияФормы = Новый Структура;
				ПараметрыОткрытияФормы.Вставить("ИмяФормы", ИмяФормы);
				ПараметрыОткрытияФормы.Вставить("ПараметрыФормы", ПараметрыФормы);
				ПараметрыОткрытияФормы.Вставить("ОбработкаОтвета", ОбработкаОтвета);
				Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
					"ИнтерактивнаяОбработкаПредупрежденийПриЗавершенииРаботы", ЭтотОбъект, ПараметрыОткрытияФормы);
			Иначе
				ДополнительныеПараметры.Вставить("ВариантФормы", "ПрикладнаяФорма");
				ОткрытьПрикладнуюФормуПредупреждения(Параметры, ОбработкаОтвета, Предупреждения[0], ИмяФормы, ПараметрыФормы);
			КонецЕсли;
		Иначе
			ДополнительныеПараметры.Вставить("ВариантФормы", "СтандартнаяФорма");
			ПараметрыОткрытияФормы = Новый Структура;
			ПараметрыОткрытияФормы.Вставить("ИмяФормы", ИмяФормы);
			ПараметрыОткрытияФормы.Вставить("ПараметрыФормы", ПараметрыФормы);
			ПараметрыОткрытияФормы.Вставить("ОбработкаОтвета", ОбработкаОтвета);
			Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
				"ИнтерактивнаяОбработкаПредупрежденийПриЗавершенииРаботы", ЭтотОбъект, ПараметрыОткрытияФормы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры ОткрытьФормуПредупрежденийПриЗавершенииРаботы.
Процедура ИнтерактивнаяОбработкаПредупрежденийПриЗавершенииРаботы(Параметры, ПараметрыОткрытияФормы) Экспорт
	
	ОткрытьФорму(
		ПараметрыОткрытияФормы.ИмяФормы,
		ПараметрыОткрытияФормы.ПараметрыФормы, , , , ,
		ПараметрыОткрытияФормы.ОбработкаОтвета);
	
КонецПроцедуры

// Продолжение процедуры ПоказатьПредупреждениеИПродолжить.
Процедура ПоказатьПредупреждениеИПродолжитьЗавершение(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат.Значение = "Завершить" Тогда
			Параметры.Отказ = Истина;
		ИначеЕсли Результат.Значение = "Перезапустить" Тогда
			Параметры.Отказ = Истина;
			Параметры.Перезапустить = Истина;
		КонецЕсли;
	КонецЕсли;
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Показывает пользователю диалог подтверждения выхода из программы.
Процедура ВопросПользователюПередЗавершениемРаботыСистемы(Параметры, ОбработкаОтвета) Экспорт
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("КодВозвратаДиалога.Да",  НСтр("ru = 'Завершить'"));
	Кнопки.Добавить("КодВозвратаДиалога.Нет", НСтр("ru = 'Отмена'"));
	
	ПараметрыВопроса = ПараметрыВопросаПользователю();
	ПараметрыВопроса.БлокироватьВесьИнтерфейс = Истина;
	ПараметрыВопроса.КнопкаПоУмолчанию = "КодВозвратаДиалога.Да";
	ПараметрыВопроса.Заголовок = НСтр("ru = 'Завершение работы'");
	ПараметрыВопроса.БольшеНеЗадаватьЭтотВопрос = Ложь;
	
	ПоказатьВопросПользователю(ОбработкаОтвета, НСтр("ru = 'Завершить работу с программой?'"), Кнопки, ПараметрыВопроса);
	
КонецПроцедуры

// Формирует отображение одного вопроса.
//
//	Если в ПредупреждениеПользователю есть свойство "ТекстГиперссылки", то открывается "ФормаИндивидуальногоОткрытия" из
//	Структуры вопроса.
//	Если в ПредупреждениеПользователю есть свойство "ТекстФлажка", то открывается форма
//	"ОбщаяФорма.ВопросПередЗавершениемРаботыСистемы".
//
// Параметры:
//  Параметры - сквозные параметры цепочки вызовов процедуры ПередЗавершениемРаботыСистемы.
//  ОбработкаОтвета - ОписаниеОповещения для продолжения после получения ответа пользователя.
//  ПредупреждениеПользователю - Структура - структура передаваемого предупреждения.
//  ИмяФормы - Строка - имя общей формы с вопросами.
//  ПараметрыФормы - Структура - параметры для формы с вопросами.
//
Процедура ОткрытьПрикладнуюФормуПредупреждения(Параметры, ОбработкаОтвета, ПредупреждениеПользователю, ИмяФормы, ПараметрыФормы)
	
	ТекстГиперссылки = "";
	Если НЕ ПредупреждениеПользователю.Свойство("ТекстГиперссылки", ТекстГиперссылки) Тогда
		Возврат;
	КонецЕсли;
	Если ПустаяСтрока(ТекстГиперссылки) Тогда
		Возврат;
	КонецЕсли;
	
	ДействиеПриНажатииГиперссылки = Неопределено;
	Если НЕ ПредупреждениеПользователю.Свойство("ДействиеПриНажатииГиперссылки", ДействиеПриНажатииГиперссылки) Тогда
		Возврат;
	КонецЕсли;
	
	ДействиеГиперссылка = ПредупреждениеПользователю.ДействиеПриНажатииГиперссылки;
	Форма = Неопределено;
	
	Если ДействиеГиперссылка.Свойство("ПрикладнаяФормаПредупреждения", Форма) Тогда
		ПараметрыФормы = Неопределено;
		Если ДействиеГиперссылка.Свойство("ПараметрыПрикладнойФормыПредупреждения", ПараметрыФормы) Тогда
			Если ТипЗнч(ПараметрыФормы) = Тип("Структура") Тогда 
				ПараметрыФормы.Вставить("ЗавершениеРаботыПрограммы", Истина);
			ИначеЕсли ПараметрыФормы = Неопределено Тогда 
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("ЗавершениеРаботыПрограммы", Истина);
			КонецЕсли;
			
			ПараметрыФормы.Вставить("ЗаголовокКнопкиДа",  НСтр("ru = 'Завершить'"));
			ПараметрыФормы.Вставить("ЗаголовокКнопкиНет", НСтр("ru = 'Отмена'"));
			
		КонецЕсли;
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("ИмяФормы", Форма);
		ПараметрыОткрытияФормы.Вставить("ПараметрыФормы", ПараметрыФормы);
		ПараметрыОткрытияФормы.Вставить("ОбработкаОтвета", ОбработкаОтвета);
		Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
			"ИнтерактивнаяОбработкаПредупрежденийПриЗавершенииРаботы", ЭтотОбъект, ПараметрыОткрытияФормы);
		
	ИначеЕсли ДействиеГиперссылка.Свойство("Форма", Форма) Тогда 
		ПараметрыФормы = Неопределено;
		Если ДействиеГиперссылка.Свойство("ПараметрыФормы", ПараметрыФормы) Тогда
			Если ТипЗнч(ПараметрыФормы) = Тип("Структура") Тогда 
				ПараметрыФормы.Вставить("ЗавершениеРаботыПрограммы", Истина);
			ИначеЕсли ПараметрыФормы = Неопределено Тогда 
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("ЗавершениеРаботыПрограммы", Истина);
			КонецЕсли;
		КонецЕсли;
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("ИмяФормы", Форма);
		ПараметрыОткрытияФормы.Вставить("ПараметрыФормы", ПараметрыФормы);
		ПараметрыОткрытияФормы.Вставить("ОбработкаОтвета", ОбработкаОтвета);
		Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
			"ИнтерактивнаяОбработкаПредупрежденийПриЗавершенииРаботы", ЭтотОбъект, ПараметрыОткрытияФормы);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке, Событие, ПрекратитьРаботу = Ложь)
	
	Если Событие = "Запуск" Тогда
		Если ПрекратитьРаботу Тогда
			Параметры.Отказ = Истина;
			Параметры.ОбработкаПродолжения = Параметры.ОбработкаЗавершения;
		КонецЕсли;
	Иначе
		ДополнительныеПараметры = Новый Структура(
			"Параметры, ОбработкаПродолжения", Параметры, Параметры.ОбработкаПродолжения);
		
		Параметры.ОбработкаПродолжения = Новый ОписаниеОповещения(
			"ДействияПередЗавершениемРаботыСистемыПослеОбработкиОшибки", ЭтотОбъект, ДополнительныеПараметры);
	КонецЕсли;
	
	НачалоОписанияОшибки = СтандартныеПодсистемыВызовСервера.ЗаписатьОшибкуВЖурналРегистрацииПриЗапускеИлиЗавершении(
		ПрекратитьРаботу, Событие, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));	
	
	ТекстПредупреждения = НачалоОписанияОшибки + Символы.ПС
		+ НСтр("ru = 'Техническая информация об ошибке записана в журнал регистрации.'")
		+ Символы.ПС + Символы.ПС
		+ КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	
	ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
		"ПоказатьПредупреждениеИПродолжить",
		СтандартныеПодсистемыКлиент.ЭтотОбъект,
		ТекстПредупреждения);
	
	Параметры.ИнтерактивнаяОбработка = ИнтерактивнаяОбработка;
	
КонецПроцедуры

Процедура УстановитьПараметрыФункциональныхОпцийИнтерфейсаПриЗапуске()
	ОпцииИнтерфейса = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске().ОпцииИнтерфейса;
	Если ТипЗнч(ОпцииИнтерфейса) = Тип("ФиксированнаяСтруктура") Тогда
		#Если ВебКлиент Тогда
			Структура = Новый Структура;
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Структура, ОпцииИнтерфейса, Истина);
			ОпцииИнтерфейса = Структура;
		#Иначе
			ОпцииИнтерфейса = Новый Структура(ОпцииИнтерфейса);
		#КонецЕсли
	КонецЕсли;
	// Установка параметров функциональных опций производится только тогда, когда они заданы.
	Если ОпцииИнтерфейса.Количество() > 0 Тогда
		УстановитьПараметрыФункциональныхОпцийИнтерфейса(ОпцииИнтерфейса);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьПомещениеФайлаПриПодключенииРасширенияРаботыСФайлами(РасширениеПодключено, ДополнительныеПараметры) Экспорт
	
	ОбработчикЗавершения = ДополнительныеПараметры.ОбработчикЗавершения;
	ИдентификаторФормы = ДополнительныеПараметры.ИдентификаторФормы;
	НачальноеИмяФайла = ДополнительныеПараметры.НачальноеИмяФайла;
	ПараметрыДиалога = ДополнительныеПараметры.ПараметрыДиалога;
	
	Если Не РасширениеПодключено Тогда
		Обработчик = Новый ОписаниеОповещения("ОбработатьРезультатПомещенияФайла", ЭтотОбъект, ОбработчикЗавершения);
		НачатьПомещениеФайла(Обработчик, , НачальноеИмяФайла, Истина, ИдентификаторФормы);
		Возврат;
	КонецЕсли;
	
	Если ПараметрыДиалога = Неопределено Тогда
		ПараметрыДиалога = Новый Структура;
	КонецЕсли;
	Если ПараметрыДиалога.Свойство("Режим") Тогда
		Режим = ПараметрыДиалога.Режим;
		Если Режим = РежимДиалогаВыбораФайла.ВыборКаталога Тогда
			ВызватьИсключение НСтр("ru = 'Выбор каталога не поддерживается'");
		КонецЕсли;
	Иначе
		Режим = РежимДиалогаВыбораФайла.Открытие;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(Режим);
	Диалог.ПолноеИмяФайла = НачальноеИмяФайла;
	ЗаполнитьЗначенияСвойств(Диалог, ПараметрыДиалога);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьРезультатПомещенияФайлов", ЭтотОбъект, ОбработчикЗавершения);
	
	Если ИдентификаторФормы <> Неопределено Тогда
		НачатьПомещениеФайлов(ОписаниеОповещения, , Диалог, Истина, ИдентификаторФормы);
	Иначе
		НачатьПомещениеФайлов(ОписаниеОповещения, , Диалог, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьРезультатПомещенияФайлов(ПомещенныеФайлы, ОбработчикЗавершения) Экспорт
	ВыборВыполнен = ПомещенныеФайлы <> Неопределено;
	ОбработатьРезультатПомещенияФайла(ВыборВыполнен, ПомещенныеФайлы, Неопределено, ОбработчикЗавершения);
КонецПроцедуры

Процедура ОбработатьРезультатПомещенияФайла(ВыборВыполнен, АдресИлиРезультатВыбора, ВыбранноеИмяФайла, ОбработчикЗавершения) Экспорт
	Если ВыборВыполнен = Истина Тогда
		Если ТипЗнч(АдресИлиРезультатВыбора) = Тип("Массив") Тогда
			ПомещенныеФайлы = АдресИлиРезультатВыбора;
		Иначе
			ОписаниеФайла = Новый Структура;
			ОписаниеФайла.Вставить("Хранение", АдресИлиРезультатВыбора);
			ОписаниеФайла.Вставить("Имя",      ВыбранноеИмяФайла);
			ПомещенныеФайлы = Новый Массив;
			ПомещенныеФайлы.Добавить(ОписаниеФайла);
		КонецЕсли;
	Иначе
		ПомещенныеФайлы = Неопределено;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОбработчикЗавершения, ПомещенныеФайлы);
КонецПроцедуры

Функция ТребуетсяПоказРекомендацииПоОбъемуОперативнойПамяти()
	ПараметрыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	Возврат ПараметрыКлиента.ТребуетсяПоказРекомендацииПоОбъемуОперативнойПамяти;
КонецФункции

Процедура ОповеститьОНехваткеПамяти() Экспорт
	РекомендуемыйОбъем = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске().РекомендуемыйОбъемОперативнойПамяти;
	
	Заголовок = НСтр("ru = 'Скорость работы снижена'");
	Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Рекомендуется увеличить
		| объем памяти до %1 Гб.'"), РекомендуемыйОбъем);
	
	ПоказатьОповещениеПользователя(Заголовок, 
	"e1cib/app/Обработка.РекомендацияПоПовышениюСкоростиРаботы",
	Текст, БиблиотекаКартинок.Предупреждение32);
КонецПроцедуры

#КонецОбласти

